class Mech_TitleMenu : CustomMenuBase
{

	void DStr(String str, vector2 pos, vector2 scale=(1.15,1.15))
	{
		MD_SBS.DrawString("MechFont", str,pos,0,Font.CR_WHITE,1.0,scale);
	}

	override void Init(Menu parent, ListMenuDescriptor desc)
	{
		Super.Init(parent, desc);
		
		MainBG = MenuTexture.Create("mTITLE",(0,0),(1.3,1.2));
		mMouse = Mouse.Create("", 0,0,32,32);
		
		buttons.push(Button.Create("mBTNEW"  ,  "mBLNEW" , 1578, 100, 1.3, 1.2));
		buttons.push(Button.Create("mBTLOAD" ,  "mBLLOAD", 1578, 274, 1.3, 1.2));
		buttons.push(Button.Create("mBTOPTS" ,  "mBLOPTS", 1578, 447, 1.3, 1.2));
		buttons.push(Button.Create("mBTCTRLS", "mBLCTRLS", 1578, 629, 1.3, 1.2));
		buttons.push(Button.Create("mBTCREDS", "mBLCREDS", 1578, 804, 1.3, 1.2));
		buttons.push(Button.Create("mBTEXIT" ,  "mBLEXIT", 1583, 982, 1.3, 1.2));
		
		for(int i = 0; i < buttons.Size(); i++)
		{
			 buttons[i].ScaleCollider(0.9, 0.7);
		}
		DontDim = true;
	}
	override void Drawer()
	{
		if(gamestate == GS_LEVEL) 
		{
			Close();
			Menu.SetMenu('MainMenu_InGame');
			return;
		}

		MD_SBS.Dim(Color(255,0,0,0), 1.0);
		super.Drawer();
		
		vector2 ht_pos = (75,1000);
		for(int i = 0; i < buttons.Size(); i++)
		{
			// buttons[i].DrawCollider();
			if(buttons[i].hovered)
			{
				Switch(i)
				{
					case 0:	 	// New Game
						DStr("Start a new game",ht_pos);
					break;
					case 1:		// Load Game
						DStr("Load a saved game",ht_pos);
					break;
					case 2:     // Options
						DStr("Options menu",ht_pos);
					break;
					case 3: 	// Controls
						DStr("Setup MechDoomer's Controls",ht_pos);
					break;
					case 4: 	// Credits
						DStr("Credits",ht_pos);
					break;
					case 5:		// Exit
						DStr("Exit game",ht_pos);
					break;
					default:
					break;
				}
			}
		}
		
		MD_SBS.DrawString("BattleTechFont", "version " .. mechdoomer_version, (5,5), 0, Font.CR_BLUE, 1.0, (0.75,0.75));   
 
		// MD_SBS.DrawRect(mMouse.collider,Color(255,127,64,127), false);
		// mMouse.DrawDebug();
		// buttons[0].DrawDebug();
	}
	
	override void Ticker()
	{
		if(btn_selected != -1) mMouse.left_pressed = true;
	
		for(int i = 0; i < buttons.Size(); i++)
		{
			buttons[i].Update(mMouse);
				
			if(buttons[i].clicked)
			{
				MenuSound("UI/PressButton");
				buttons[i].clicked = false;
				
				Switch(i)
				{
					case 0:	 	// New Game
						Menu.SetMenu('PlayerclassMenu');
					break;
					case 1:		// Load Game
						Menu.SetMenu('LoadGameMenu');
					break;
					case 2:     // Options
						Menu.SetMenu('OptionsMenu');
					break;
					case 3: 	// Controls
						Menu.SetMenu("MechDoomerControlsMenu");
					break;
					case 4: 	// Credits
						Menu.SetMenu("MechDoomerCredits");
					break;
					case 5:		// Exit
						Menu.SetMenu("QuitMenu");
					break;
					default:
					break;
				}
			}
		}
	}
}

// In-Game menu system
class Mech_InGameMenu : CustomMenuBase
{	
	override TextElement newBtnText(String txt)
	{
		return TextElement.Create("MechFont", txt, (0,0), (1.0, 1.0), Font.CR_GRAY);
	}

	override void Init(Menu parent, ListMenuDescriptor desc)
	{
		Super.Init(parent, desc);
		MainBG = MenuTexture.Create("mTITLE2",(10,0),(1.6,1.5));
		mMouse = Mouse.Create("", 0,0,32,32);
		TextElement btntxt = newBtnText("NEW MERCENARY");
		buttons.push(Button.Create("","",777,220,0.2,0.1,btntxt));
		
		btntxt = newBtnText("LOAD GAME");
		buttons.push(Button.Create("","",830,320,0.2,0.1,btntxt));
		
		btntxt = newBtnText("SAVE GAME");
		buttons.push(Button.Create("","",830,420,0.2,0.1,btntxt));
		
		btntxt = newBtnText("COMBAT VARIABLES");
		buttons.push(Button.Create("","",735,520,0.2,0.1,btntxt));
		
		btntxt = newBtnText("COCKPIT CONTROLS");
		buttons.push(Button.Create("","",735,620,0.2,0.1,btntxt));
		
		btntxt = newBtnText("FLEE TO DOS");
		buttons.push(Button.Create("","",810,760,0.2,0.1,btntxt));
		
		for(int i = 0; i < buttons.Size(); i++)
		{
			 buttons[i].SetupCollider(buttons[i].btnrect.pos.x+30, buttons[i].btnrect.pos.y+25,480,60);
		}
		
		DontBlur = true;
		DontDim = true;
	}
	
	override void Ticker()
	{
		if(btn_selected != -1) mMouse.left_pressed = true;
	
		for(int i = 0; i < buttons.Size(); i++)
		{
			buttons[i].Update(mMouse);
			
			if(buttons[i].pressed)
			{
				buttons[i].btntext.font_color = Font.CR_RED;
			}
			else
				buttons[i].btntext.font_color = Font.CR_GRAY;
			
			if(buttons[i].clicked)
			{
				if(i != 2) MenuSound("UI/PressButton");
				buttons[i].clicked = false;
				
				Switch(i)
				{
					case 0:	 	// New Game
						Menu.SetMenu('PlayerclassMenu');
					break;
					case 1:		// Load Game
						Menu.SetMenu('LoadGameMenu');
					break;
					case 2:		// Save Game
						if(CanSave()) 
						{
							MenuSound("UI/PressButton");
							Menu.SetMenu('SaveGameMenu');
						}
						else
							MenuSound("UI/Error");
					break;
					case 3:     // Options
						Menu.SetMenu('OptionsMenu');
					break;
					case 4:		// Controls
						Menu.SetMenu("MechDoomerControlsMenu");
					break;
					case 5:		// Exit
						Menu.SetMenu("QuitMenu");
					break;
					default:
					break;
				}
			}
		}
	}
}