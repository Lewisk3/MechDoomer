class Mech_TitleMenu : CustomMenuBase
{
	override TextElement newBtnText(String txt)
	{
		return TextElement.Create(graphics,txt, (0,0), (1.35,2.0), Font.CR_BRICK);
	}

	override void Init(Menu parent, ListMenuDescriptor desc)
	{
		Super.Init(parent, desc);
		
		graphics = MenuDrawer.Create("MechFont", 1920,1080);
		MainBG = MenuTexture.Create(graphics,"mTITLE",(0,0),(1.0,1.0));
		mMouse = Mouse.Create(graphics,0,0,32,32);
		
		buttons.push(Button.Create(graphics,  "mBTNEW",  "mBLNEW",  1315, 16,0.29,0.16));
		buttons.push(Button.Create(graphics, "mBTLOAD",  "mBLLOAD", 1315,190,0.29,0.16));
		buttons.push(Button.Create(graphics, "mBTOPTS",  "mBLOPTS", 1315,363,0.29,0.16));
		buttons.push(Button.Create(graphics,"mBTCTRLS", "mBLCTRLS", 1315,545,0.29,0.16));
		buttons.push(Button.Create(graphics,"mBTCREDS", "mBLCREDS", 1315,720,0.29,0.16));
		buttons.push(Button.Create(graphics, "mBTEXIT",  "mBLEXIT", 1320,898,0.29,0.16));
		
		for(int i = 0; i < buttons.Size(); i++)
		{
			 buttons[i].SetupCollider(buttons[i].btnrect.pos.x+30, buttons[i].btnrect.pos.y+25,495,120);
		}
		DontDim = true;
	}
	override void Drawer()
	{
		if(gamestate == GS_LEVEL) 
		{
			Close();
			Menu.SetMenu('MainMenu_InGame');
			return;
		}

		graphics.Dim(Color(255,0,0,0), 1.0);
		super.Drawer();
		
		vector2 ht_pos = (75,500);
		for(int i = 0; i < buttons.Size(); i++)
		{
			if(buttons[i].hovered)
			{
				// buttons[i].DrawCollider();
			
				Switch(i)
				{
					case 0:	 	// New Game
						graphics.DrawString("Start a new game",ht_pos,0,Font.CR_WHITE,(0.5,0.5));
					break;
					case 1:		// Load Game
						graphics.DrawString("Load a saved game",ht_pos,0,Font.CR_WHITE,(0.5,0.5));
					break;
					case 2:     // Options
						graphics.DrawString("Options menu",ht_pos,0,Font.CR_WHITE,(0.5,0.5));
					break;
					case 3: 	// Controls
						graphics.DrawString("Setup MechDoomer's Controls",ht_pos,0,Font.CR_WHITE,(0.5,0.5));
					break;
					case 4: 	// Credits
						graphics.DrawString("Credits",ht_pos,0,Font.CR_WHITE,(0.5,0.5));
					break;
					case 5:		// Exit
						graphics.DrawString("Exit game",ht_pos,0,Font.CR_WHITE,(0.5,0.5));
					break;
					default:
					break;
				}
			}
		}
		
		graphics.SetFont("BattleTechFont");
		graphics.DrawString("version " .. mechdoomer_version, (5,5), 0, Font.CR_BLUE, (0.75,0.75));   
		graphics.SetFont("MechFont");
		
		// mMouse.DrawDebug();
		// buttons[0].DrawDebug();
	}
	
	override void Ticker()
	{
		if(btn_selected != -1) mMouse.left_pressed = true;
	
		for(int i = 0; i < buttons.Size(); i++)
		{
			buttons[i].Update(mMouse);
				
			if(buttons[i].clicked)
			{
				MenuSound("UI/PressButton");
				buttons[i].clicked = false;
				
				Switch(i)
				{
					case 0:	 	// New Game
						Menu.SetMenu('PlayerclassMenu');
					break;
					case 1:		// Load Game
						Menu.SetMenu('LoadGameMenu');
					break;
					case 2:     // Options
						Menu.SetMenu('OptionsMenu');
					break;
					case 3: 	// Controls
						Menu.SetMenu("MechDoomerControlsMenu");
					break;
					case 4: 	// Credits
						Menu.SetMenu("MechDoomerCredits");
					break;
					case 5:		// Exit
						Menu.SetMenu("QuitMenu");
					break;
					default:
					break;
				}
			}
		}
	}
}

// In-Game menu system
class Mech_InGameMenu : CustomMenuBase
{	
	override TextElement newBtnText(String txt)
	{
		return TextElement.Create(graphics,txt, (0,0), (2.3,1.8), Font.CR_GRAY);
	}

	override void Init(Menu parent, ListMenuDescriptor desc)
	{
		Super.Init(parent, desc);
		graphics = MenuDrawer.Create("MechFont", 1920,1080);
		MainBG = MenuTexture.Create(graphics,"mTITLE2",(650,180),(0.35,0.65));
		mMouse = Mouse.Create(graphics,0,0,32,32);
		TextElement btntxt = newBtnText("NEW MERCENARY");
		buttons.push(Button.Create(graphics,"","",760,220,0.2,0.1,btntxt));
		
		btntxt = newBtnText("LOAD GAME");
		buttons.push(Button.Create(graphics,"","",820,320,0.2,0.1,btntxt));
		
		btntxt = newBtnText("SAVE GAME");
		buttons.push(Button.Create(graphics,"","",820,420,0.2,0.1,btntxt));
		
		btntxt = newBtnText("COMBAT VARIABLES");
		buttons.push(Button.Create(graphics,"","",710,520,0.2,0.1,btntxt));
		
		btntxt = newBtnText("COCKPIT CONTROLS");
		buttons.push(Button.Create(graphics,"","",710,620,0.2,0.1,btntxt));
		
		btntxt = newBtnText("FLEE TO DOS");
		buttons.push(Button.Create(graphics,"","",800,740,0.2,0.1,btntxt));
		
		for(int i = 0; i < buttons.Size(); i++)
		{
			 buttons[i].SetupCollider(buttons[i].btnrect.pos.x+30, buttons[i].btnrect.pos.y+25,480,60);
		}
		
		DontBlur = true;
		DontDim = true;
	}
	
	override void Ticker()
	{
		if(btn_selected != -1) mMouse.left_pressed = true;
	
		for(int i = 0; i < buttons.Size(); i++)
		{
			buttons[i].Update(mMouse);
			
			if(buttons[i].pressed)
			{
				buttons[i].btntext.font_color = Font.CR_RED;
			}
			else
				buttons[i].btntext.font_color = Font.CR_GRAY;
			
			if(buttons[i].clicked)
			{
				if(i != 2) MenuSound("UI/PressButton");
				buttons[i].clicked = false;
				
				Switch(i)
				{
					case 0:	 	// New Game
						Menu.SetMenu('PlayerclassMenu');
					break;
					case 1:		// Load Game
						Menu.SetMenu('LoadGameMenu');
					break;
					case 2:		// Save Game
						if(CanSave()) 
						{
							MenuSound("UI/PressButton");
							Menu.SetMenu('SaveGameMenu');
						}
						else
							MenuSound("UI/Error");
					break;
					case 3:     // Options
						Menu.SetMenu('OptionsMenu');
					break;
					case 4:		// Controls
						Menu.SetMenu("MechDoomerControlsMenu");
					break;
					case 5:		// Exit
						Menu.SetMenu("QuitMenu");
					break;
					default:
					break;
				}
			}
		}
	}
}