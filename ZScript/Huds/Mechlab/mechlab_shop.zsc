extend class Mechlab_Menu;

struct ShopOffsets
{
	int energyoffs, ballisticoffs, missileoffs, ammooffs, engineoffs;
	int energymax, ballisticmax, missilemax, ammomax, enginemax;
	bool btn_energy, btn_ballistic, btn_missile, btn_ammo, btn_engine;
}
ShopOffsets shopstate;

Array<TempItems> selmechweaps;

int forcemaxitems;
int forceminitems;
int item_cost;
bool mechmode;

void UpdateMechWeaponsList()
{
	selmechweaps.Clear();
	if(selected_item.index < 0) return;
	let mech = mechplr.shopmechs[selected_item.index];

	for(int i = 0; i < mech.active_weapons.Size(); i++)
	{
		let storedweap = mech.active_weapons[i];
		if(!selmechweaps.Size()) 
		{
			let ti = new("TempItems");
			ti.Tag = storedweap.Tag;
			selmechweaps.Push(ti);
			continue;
		}
		else
		{
			bool found = false;
			for(int x = 0; x < selmechweaps.Size(); x++)
			{
				let selweap = selmechweaps[x];
				if(selmechweaps[x].Tag == storedweap.Tag) 
				{
					selweap.amt++;
					found = true;
					break;
				}
			}
			if(!found)
			{
				let ti = new("TempItems");
				ti.Tag = storedweap.Tag;
				selmechweaps.Push(ti);
			}
		}
	}
	// Update cost
	let defs = GetDefaultByType(mech.baseclass);
	item_cost = defs.worth;
}

int GetMaxShopItems()
{
    if(!mechmode) 
		return forcemaxitems ? forcemaxitems : mechplr.shopitems.items.Size();
	else
		return forcemaxitems ? forcemaxitems : mechplr.shopmechs.Size();
}

void XORBtns(int btnnum)
{
	mechplr.A_PlaySound("UI/PressButton",CHAN_WEAPON);
	shopstate.btn_energy    = (btnnum == 0) ? !shopstate.btn_energy    : false;
	shopstate.btn_ballistic = (btnnum == 1) ? !shopstate.btn_ballistic : false;
	shopstate.btn_missile   = (btnnum == 2) ? !shopstate.btn_missile   : false;
	shopstate.btn_ammo      = (btnnum == 3) ? !shopstate.btn_ammo      : false;
	shopstate.btn_engine    = (btnnum == 4) ? !shopstate.btn_engine    : false;
	if( mechmode && !shopstate.btn_ballistic && !shopstate.btn_missile && 
	   !shopstate.btn_ammo && !shopstate.btn_engine )
	{
		shopstate.btn_energy = true;
		ChangeShop(shopstate.energyoffs, shopstate.energymax);
	}
}

void ProcessShopButtons()
{
	bool pricevalid = item_cost && (mechplr.inv_context.money_cbills-item_cost >= 0);
	
	// Textures
	if(!mechmode)
	{
		H_DrawImage(shopstate.btn_energy ? tBtnlit_Energy : tBtn_Energy,(energybtn.x+63,energybtn.y+31),0);
		H_DrawImage(shopstate.btn_ballistic ? tBtnlit_Ballistic : tBtn_Ballistic,(ballisticbtn.x+63,ballisticbtn.y+31),0);
		H_DrawImage(shopstate.btn_missile ? tBtnlit_Missiles : tBtn_Missiles,(missilesbtn.x+61,missilesbtn.y+31),0);
		H_DrawImage(shopstate.btn_ammo ? tBtnlit_Ammo : tBtn_Ammo,(ammobtn.x+61,ammobtn.y+31),0);
		H_DrawImage(shopstate.btn_engine ? tBtnlit_Engines : tBtn_Engines,(enginesbtn.x+61,enginesbtn.y+31),0);
	}
	else
	{
		H_DrawImage(shopstate.btn_energy ? tBtnlit_All : tBtn_All,(energybtn.x+63,energybtn.y+31),0);
		H_DrawImage(shopstate.btn_ballistic ? tBtnlit_Light : tBtn_Light,(ballisticbtn.x+63,ballisticbtn.y+31),0);
		H_DrawImage(shopstate.btn_missile ? tBtnlit_Medium : tBtn_Medium,(missilesbtn.x+61,missilesbtn.y+31),0);
		H_DrawImage(shopstate.btn_ammo ? tBtnlit_Heavy : tBtn_Heavy,(ammobtn.x+61,ammobtn.y+31),0);
		H_DrawImage(shopstate.btn_engine ? tBtnlit_Assault : tBtn_Assault,(enginesbtn.x+61,enginesbtn.y+31),0);
	}
	H_DrawImage( (checkCollision(mouse, buyitembtn)&&pricevalid&&!mechmode) ? tBtnlit_BuyItem : tBtn_BuyItem,(buyitembtn.x+61,buyitembtn.y+31),0);
	H_DrawImage( (checkCollision(mouse, buymechbtn)&&pricevalid&& mechmode) ? tBtnlit_BuyMech : tBtn_BuyMech,(buymechbtn.x+68,buymechbtn.y+31),0);
	H_DrawImage(mechmode ? tBtnlit_Mechsmode : tBtn_Mechsmode,(mechsmodebtn.x+120,mechsmodebtn.y+31),0);
	
	// Scroll
	if(mstats.scroll == -1)
	{
		invoffset -= mstats.right_btn ? maxpageitems : 1;
		if(invoffset < forceminitems) invoffset = forceminitems;
		mstats.scroll = 0;
	}
	if(mstats.scroll == 1)
	{
		invoffset += mstats.right_btn ? maxpageitems : 1;
		if(invoffset+maxpageitems > GetMaxShopItems()) 
			invoffset = clamp(GetMaxShopItems()-maxpageitems, forceminitems, GetMaxShopItems()); 
		mstats.scroll = 0;
	}
	
	if( mstats.left_btn && !messagebox_active)
	{
		if( checkCollision(mouse, mechsmodebtn) )
		{
			mechplr.A_PlaySound("UI/PressButton",CHAN_WEAPON);
			mechmode = !mechmode;
			
			if(MechMode) 
				MechsInit();
			else
				ShopInit();
			mstats.left_btn = false;
		}
	
		if( checkCollision(mouse, buyitembtn) && pricevalid && selected_item.index >= 0 && !mechmode)
		{
			mechplr.A_PlaySound("UI/PressButton",CHAN_WEAPON);
			MechDoomerEvents.SendNetworkEvent("shop_buyitem",selected_item.index, item_cost);
			mstats.left_btn = false;
			mechplr.sendHudAlert("Item purchased: \n"..selected_item.Tag,Font.CR_GREEN);
		}
		if( checkCollision(mouse, buymechbtn) && pricevalid && selected_item.index >= 0 &&  mechmode)
		{
			let mech = mechplr.shopmechs[selected_item.index];
			let defs = GetDefaultByType(mech.baseclass);
			mechplr.A_PlaySound("UI/PressButton",CHAN_WEAPON);
			MechDoomerEvents.SendNetworkEvent("shop_buymech",selected_item.index, item_cost);
			mstats.left_btn = false;
			mechplr.sendHudAlert("Mech purchased: \n"..PlayerPawn.GetPrintableDisplayName(mech.baseclass),Font.CR_GREEN);
		}
		
		if( checkCollision(mouse, energybtn) )
		{
			XORBtns(0);
			if(shopstate.btn_energy) 
				ChangeShop(shopstate.energyoffs, shopstate.energymax);
			else if(!mechmode)
				ChangeShop(0, 0);
				
			mstats.left_btn = false;
		}
		if( checkCollision(mouse, ballisticbtn) )
		{
			XORBtns(1);
			if(shopstate.btn_ballistic) 
				ChangeShop(shopstate.ballisticoffs, shopstate.ballisticmax);
			else if(!mechmode)
				ChangeShop(0, 0);
			mstats.left_btn = false;
		}
		if( checkCollision(mouse, missilesbtn) )
		{
			XORBtns(2);
			if(shopstate.btn_missile) 
				ChangeShop(shopstate.missileoffs, shopstate.missilemax);
			else if(!mechmode)
				ChangeShop(0, 0);
			mstats.left_btn = false;
		}
		if( checkCollision(mouse, ammobtn) )
		{
			XORBtns(3);
			if(shopstate.btn_ammo) 
				ChangeShop(shopstate.ammooffs, shopstate.ammomax);
			else if(!mechmode)
				ChangeShop(0, 0);
			mstats.left_btn = false;
		}
		if( checkCollision(mouse, enginesbtn) )
		{
			XORBtns(4);
			if(shopstate.btn_engine) 
				ChangeShop(shopstate.engineoffs, shopstate.enginemax);
			else if(!mechmode)
				ChangeShop(0, 0);
			mstats.left_btn = false;
		}
	}
}

void ChangeShop(int newoffs, int newmax)
{
	invoffset = newoffs;
	forceminitems = newoffs;
	forcemaxitems = newmax ? newoffs+newmax : 0;
}

void ShopInit()
{
	shopstate.energyoffs    = mechplr.shopitems.energy_min;
	shopstate.ballisticoffs = mechplr.shopitems.ballistics_min;
	shopstate.missileoffs   = mechplr.shopitems.missiles_min;
	shopstate.ammooffs      = mechplr.shopitems.ammo_min;
	shopstate.engineoffs    = mechplr.shopitems.engines_min;
	shopstate.energymax    = mechplr.shopitems.energy_max-1;
	shopstate.ballisticmax = mechplr.shopitems.ballistics_max-1;
	shopstate.missilemax   = mechplr.shopitems.missiles_max-1;
	shopstate.ammomax      = mechplr.shopitems.ammo_max-1;
	shopstate.enginemax    = mechplr.shopitems.engines_max-1;
	
	ClearSelected();
	invoffset = 0;
	forceminitems = 0;
	forcemaxitems = 0;
	maxpageitems = 25;
	
	shopstate.btn_energy    = false;
	shopstate.btn_ballistic = false;
	shopstate.btn_missile   = false;
	shopstate.btn_ammo      = false;
	shopstate.btn_engine    = false;
}

void MechsInit()
{
	int l, m, h, a;
	[l, m, h, a] = mechplr.GetMechSortIndexes();
	
	shopstate.energyoffs    = 0;
	shopstate.ballisticoffs = 0;
	shopstate.missileoffs   = l;
	shopstate.ammooffs      = m;
	shopstate.engineoffs    = h;
	shopstate.energymax    = 0;
	shopstate.ballisticmax = l;
	shopstate.missilemax   = m-l;
	shopstate.ammomax      = h-m;
	shopstate.enginemax    = 0;
	
	ClearSelected();
	invoffset = 0;
	forceminitems = 0;
	forcemaxitems = 0;
	maxpageitems = 25;
	
	shopstate.btn_energy    =  true;   // All
	shopstate.btn_ballistic = false;
	shopstate.btn_missile   = false;
	shopstate.btn_ammo      = false;
	shopstate.btn_engine    = false;
}

void drawShopInfo()
{
	String weapston = MDMath.GetPrintableFloat(mechplr.inv_context.weapons_tonnage);
	String armston = MDMath.GetPrintableFloat(mechplr.inv_context.armor_tonnage);
	String mechston = MDMath.GetPrintableFloat(mechplr.mech_tonnage);
	String heateff = MDMath.GetPrintableFloat(mechplr.HeatEfficiency*35,2);
	String maxspd = MDMath.GetPrintableFloat(mechplr.MoveSpeedKPH,2);
	string enginetype = mechplr.inv_context.engine ? mechplr.inv_context.engine.displayName : "INVALID ENGINE";

	if(mechplr.CheckCompensative()) 
		maxspd = MDMath.GetPrintableFloat(double(mechplr.MoveSpeedKPH) * mechplr.overfactor,2,0.01);
	
	vector2 detailpos = (280,300);
	H_DrawString("- Active Mech -",detailpos);
	H_DrawString("Chassis: " .. mechplr.getChassisName(),(detailpos.x,detailpos.y+25));
	H_DrawString("Tonnage: " .. weapston .. "/" .. mechston,(detailpos.x,detailpos.y+50));
	H_DrawString("Armor: " .. armston .. " tons",(detailpos.x,detailpos.y+75));
	H_DrawString("Engine: " .. enginetype,(detailpos.x,detailpos.y+100));
	H_DrawString("Heat Efficiency: " .. heateff,(detailpos.x,detailpos.y+125));
	H_DrawString("Max Speed: " .. maxspd .. "Kph",(detailpos.x,detailpos.y+150));
	
	vector2 cbillpos  = (280,110);
	double cbills_remain = mechplr.inv_context.money_cbills-item_cost;
	String balstr = (cbills_remain < 0) ? "INVALID" : int(cbills_remain).."";
	H_DrawString("Funds: ".. mechplr.inv_context.money_cbills, cbillpos);
	H_DrawString("Price: ".. item_cost, (cbillpos.x, cbillpos.y+25) );
	H_DrawString("Bal: ".. balstr, (cbillpos.x, cbillpos.y+50) );
	
	if(invoffset > forceminitems)H_DrawImage(tInv_ArrowUp,(1309,188),0);
	if(invoffset+maxpageitems < GetMaxShopItems())H_DrawImage(tInv_ArrowDown,(1307,974),0);

	String mode = checkCollision(mouse,modebtn) ?  tBtnlit_WeaponsLab : tBtn_WeaponsLab;
	H_DrawImage(mode,(590,782),0);

	drawShopInventory(1330,235, invoffset, maxpageitems);
	ProcessShopButtons();
	drawShopItemInfo();
}


void drawShopInfoLine(String txt, int inline, int linesize=25, vector2 detailpos=(790,120))
{
	drawInfoLine(txt,inline,linesize,detailpos);
}
void drawShopItemInfo()
{	
	if(selected_item.index < 0) return;
	if(!mechmode)
	{
		mw_InvStruct item;
		item = mechplr.shopitems.items[selected_item.index];
		
		if(!item && !selected_item.mWeapon) item_cost = 0; 
		
		if(selected_item.mWeapon)
		{
			class<Mechbase_Weapon> invweap = selected_item.mWeapon;
			let weapdefs = GetDefaultByType(invweap);
			String wtons = weapdefs.weight.."";
			let weaptypedefs = GetDefaultByType(GetDefaultByType(invweap).weapType);
			String wheat = weaptypedefs.heatgen .. "";
			wtons.truncate(wtons.Length()-4);
			wheat.truncate(wheat.Length()-4);
			drawShopInfoLine("Name: " .. selected_item.Tag, 0);
			drawShopInfoLine("Damage: " .. weaptypedefs.truedamage,1);
			drawShopInfoLine("Heat: " .. wheat,2);
			drawShopInfoLine("Nickname: " .. weapdefs.displayName, 3);
			drawShopInfoLine("Sell Value: " .. weapdefs.worth, 4);
			drawShopInfoLine("Weight: " .. wtons .. " tons", 5);
			drawShopInfoLine("Criticals: " .. weapdefs.hardslots, 6);
			int shopline = 6;
			if(weaptypedefs.minimum_range) drawShopInfoLine("Min Range: " .. int(weaptypedefs.minimum_range),++shopline);
			if(weaptypedefs.maximum_range) drawShopInfoLine("Max Range: " .. int(weaptypedefs.maximum_range),++shopline);
			item_cost = GetDefaultByType(invweap).worth*2;
		}
		
		if(item && !selected_item.mWeapon)
		{
			item_cost = item.worth*2;

			// Standard Descriptions
			int i = 0;
			for(i = 0; i < item.description.Size(); i++) drawShopInfoLine(item.description[i],i);
		
			// Real-Time Descriptions
			class<Mechbase_Engine> eng = item.Tag;
			if(eng)
			{
				String dname;
				int non, type;
				[dname, type, non, non, non] = Mechbase_Engine.GetBasics(eng);
				string spd = ""..mechplr.EngineGetKPH(type);
				spd.truncate(spd.Length()-4);
				drawShopInfoLine("Run Speed: "..spd.."KPH",i);
			}
			class<Mechbase_Ammo> am = item.Tag;
			if(am) drawShopInfoLine("Ammunition: "..item.ammunition,i);
		}
	}
	else
	{	
		let mech = mechplr.shopmechs[selected_item.index];
		let mechdefs = GetDefaultByType(mech.baseclass);
		if(!mech) return;
	
		String weapston = (mech.inv_defs.weapons_tonnage .. "");
		String armston = (mech.inv_defs.armor_tonnage .. "");
		String mechston = (mechdefs.mech_tonnage .. "");
		String heateff = mech.inv_defs.heateff*35 .. "";
		String maxspd = mech.inv_defs.MoveSpeedKPH .. "";
		string enginetype = mech.inv_defs.engine ? mech.inv_defs.engine.displayName : "INVALID ENGINE";
		
		maxspd.truncate(maxspd.Length()-4);
		weapston.truncate(weapston.length()-3);
		armston.truncate(armston.length()-3);
		heateff.truncate(heateff.Length()-3);
		mechston.truncate(mechston.length()-3);
		enginetype.truncate(enginetype.Length()-7);
		
		vector2 detailpos = (280,390);
		H_DrawString("- Selected Mech -", (detailpos.x,detailpos.y+100));
		H_DrawString("Chassis: " .. mechdefs.getClassName(),(detailpos.x,detailpos.y+125));
		H_DrawString("Tonnage: " .. weapston .. "/" .. mechston,(detailpos.x,detailpos.y+150));
		H_DrawString("Armor: " .. armston .. " tons",(detailpos.x,detailpos.y+175));
		H_DrawString("Engine: " .. enginetype,(detailpos.x,detailpos.y+200));
		H_DrawString("Heat Efficiency: " .. heateff,(detailpos.x,detailpos.y+225));
		H_DrawString("Max Speed: " .. maxspd .. "Kph",(detailpos.x,detailpos.y+250));
		drawShopInfoLine("Weapons: ",19);
		for(int i = 0; i < selmechweaps.Size(); i++)
		{
			String disp = selmechweaps[i].Tag;
			if(selmechweaps[i].amt) disp = disp .. " x" .. selmechweaps[i].amt+1;
			drawShopInfoLine(disp,20+i);
		}
	}
}

// Inventory
void drawShopInventory(int px, int py, int start, int max)
{
	int itemcount = GetMaxShopItems();
	max = start+max > itemcount ? itemcount : start+max;
	for(int i = start; i < max; i++)
	{
		if(!mechmode)
		{
			let item = mechplr.shopitems.items[i];
			String weapname = item.Tag;
			class<Mechbase_Weapon> mw  = weapname;
			class<Mechbase_Utility> ut = weapname;
			class<Mechbase_Engine> eng = weapname;
			
			Rect checkmouse;
			initRect(checkmouse,px-65,py+((i-start)*25),16*(weapname.Length()+3),17);
			int sely = (i-start)*25;
			
			// DrawRect(checkmouse, debug_color);
			
			int fcr = eng ? Font.CR_ORANGE : ut ? Font.CR_DARKGRAY : Font.CR_WHITE;
			if(i == selected_item.index && !selected_item.location) 
			{
				fcr = selected_item.mEngine ? Font.CR_GREEN : selected_item.mUtil ? Font.CR_SAPPHIRE : Font.CR_DARKRED;  
			}
			if(mstats.left_btn && checkCollision(mouse,checkmouse)) 
			{
				clearSelected();
				setSelected(weapname, null, ut, mw, eng, i);
			}
			string displayName = eng ? MechBase_Engine.GetRealName(eng) : weapname;
			H_DrawString(displayName, (px-68,py+sely),0,fcr);
		}
		else
		{
			let mech = mechplr.shopmechs[i];
			let defs = GetDefaultByType(mech.baseclass);
			String mechname = PlayerPawn.GetPrintableDisplayName(mech.baseclass);
			
			Rect checkmouse;
			initRect(checkmouse,px-65,py+((i-start)*25),16*(mechname.Length()+3),17);
			int sely = (i-start)*25;
			
			// DrawRect(checkmouse, debug_color);
			
			int fcr = Font.CR_WHITE;
			if(i == selected_item.index && !selected_item.location) fcr = Font.CR_SAPPHIRE;  
			if(mstats.left_btn && checkCollision(mouse,checkmouse)) 
			{
				clearSelected();
				setSelected(GetDefaultByType(mech.baseclass).getClassName(), NULL, NULL, NULL, NULL, i);
				UpdateMechWeaponsList();
			}
			H_DrawString(mechname, (px-68,py+sely),0,fcr);
		}
	}
}