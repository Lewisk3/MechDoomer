
struct MouseStatus
{
	bool left_btn;
	bool right_btn;
	bool left_pressed;
	bool right_pressed;
	int scroll;
}

struct SelectedObject
{	
	Name tag;
	int index;
	MechHardpoint location;
	Mechbase_Weapon mWeaponOBJ;
	class<Mechbase_Weapon>  mWeapon;
	class<Mechbase_Utility> mUtil;
	class<Mechbase_Engine>  mEngine;
}

class Mechlab_Menu : GenericMenu
{
	// Structs
	MouseStatus mstats;
	Vector2 mpos;
	Mechwarrior mechplr;
	FadingText plrHudMessage;
	ArmoryUtility autil_item;
	
	// Textures
	TextureID tMain;
	TextureID tArmory;
	TextureID tBtn_Highlight;
	TextureID tMech_Head;
	TextureID tMech_RTorso;
	TextureID tMech_CTorso;
	TextureID tMech_LTorso;
	TextureID tMech_RArm;
	TextureID tMech_LArm;
	TextureID tMech_RLeg;
	TextureID tMech_LLeg;
	TextureID tInv_ArrowUp;
	TextureID tInv_ArrowDown;
	TextureID tMessageBox;
	
	// Bools
	bool page_init;
	
	// Ints
	int hud_page;
	int page_offs;
	int defwidth;
	int defheight;
	int inputclear;
	int infomode;
	int invoffset;
	
	// Rectangles
	Color debug_color;
	Rect mouse, rHD, rCT, rLT, rRT, rLA, rRA, rLL, rRL, weapbox, invbox, equipbtn, dequipbtn, scrapbtn, sortbtn, modebtn, autilbtn, repairbtn;
	
	// Fonts
	HUDFont mHUDFont;
	
	// Actual Menu
	void Init(Menu parent, Mechwarrior CPlayer)
	{
		statusbar.SetSize(32, 320, 200);
		menuactive = OnNoPause;
		mParentMenu = parent;
		mechplr = CPlayer;
		mMouseCapture = true;
		mBackbuttonSelected = false;
		DontDim = false;
		debug_color = Color(255,230,16,230);
		defwidth = 1920;
		defheight = 1080;
		clearSelected();
		
		hud_page = 0;  // Mechlab Page or Armory Page
		
		tMain = TexMan.CheckForTexture("LOADMHUD", TexMan.Type_Any);
		tArmory = TexMan.CheckForTexture("LOADRHUD", TexMan.Type_Any);
		tMech_Head = TexMan.CheckForTexture("LMHUD_HD", TexMan.Type_Any);
		tMech_LTorso = TexMan.CheckForTexture("LMHUD_LT", TexMan.Type_Any);
		tMech_CTorso = TexMan.CheckForTexture("LMHUD_CT", TexMan.Type_Any);
		tMech_RTorso = TexMan.CheckForTexture("LMHUD_RT", TexMan.Type_Any);
		tMech_LArm = TexMan.CheckForTexture("LMHUD_LA", TexMan.Type_Any);
		tMech_RArm = TexMan.CheckForTexture("LMHUD_RA", TexMan.Type_Any);
		tMech_LLeg = TexMan.CheckForTexture("LMHUD_LL", TexMan.Type_Any);
		tMech_RLeg = TexMan.CheckForTexture("LMHUD_RL", TexMan.Type_Any);
		tInv_ArrowUp = TexMan.CheckForTexture("HUDARW", TexMan.Type_Any);
		tInv_ArrowDown = TexMan.CheckForTexture("HUDARWD", TexMan.Type_Any);
		tBtn_Highlight = TexMan.CheckForTexture("BTNBOX", TexMan.Type_Any);
		tMessageBox = TexMan.CheckForTexture("HMESSBG", TexMan.Type_Any);
		
		mHUDFont = HUDFont.Create("MechFont");
		hardpoint = -1;
		
		// mechplr hardpoint rects
		initRect(rHD,908,458,96,45);
		initRect(rCT,919,524,76,175);
		initRect(rLA,770,524,62,182);
		initRect(rRA,1082,524,62,182);
		initRect(rLT,850,524,62,162);
		initRect(rRT,1002,524,62,162);
		initRect(rLL,843,724,90,172);
		initRect(rRL,983,724,90,172);
		// Border rects
		initRect(weapbox,750,130,415,790);
		initRect(invbox,1198,130,375,790);
		// Action Buttons
		initRect(sortbtn,1506,143,50,20);
		initRect(scrapbtn,1491,883,75,25);
		initRect(dequipbtn,1079,459,75,25);
		initRect(equipbtn,641,300,75,25);
		initRect(autilbtn,990,407,160,30);
		initRect(repairbtn,769,407,90,30);
		
		initRect(mouse, mpos.x, mpos.y, 8,8);
	}

	override void Drawer ()
	{
		statusbar.BeginHUD();
		
		/*
		console.printf("Tag: %s",selected_item.tag.." ");
		console.printf("Index: %d",selected_item.index);
		console.printf("Location?: %d", selected_item.location!=NULL);
		console.printf("W:%d, U:%d, O:%d",selected_item.mWeapon!=NULL,selected_item.mUtil!=NULL,selected_item.mWeaponOBJ!=NULL);
		*/
		
		
		// Mechlab mode
		if(hud_page == 0)
		{
			if(!page_init)
			{
				page_offs = 1;
				initRect(modebtn,463,731,238,35);
				page_init = true;
			}
			H_DrawTexture(tMain, (942,807), 0, (0.9,0.7) );
			if( ValidateHardpoint(hardpoint) && hardpoint >= 0 ) DrawHardpointItems(hardpoint,800,160);
			drawInventory(1278,150, invoffset, 30);
			drawLoadoutBody(931,120,hardpoint);
			drawMechlabInfo();
		} 
		// Armory mode
		else if(hud_page == 1)
		{
			if(!page_init)
			{
				page_offs = -1;
				initRect(modebtn,475,600,238,35);
				page_init = true;
			}
			H_DrawTexture(tArmory, (942,807), 0, (0.9,0.7) );
			drawLoadoutBody(931,120,hardpoint);
			drawArmoryInfo();
		}
			
		// Global Actions
		if(mstats.left_btn && checkCollision(mouse,modebtn) && page_init) 
		{
			HUD_ChangePageRelative(page_offs);
		}		
		// Player produced HUD message.
		if(mechplr.hAlert.text != "")
		{
			plrHudMessage.txt = mechplr.hAlert.text;
			plrHudMessage.alpha = 0.2;
			plrHudMessage.col = mechplr.hAlert.col;
			mechplr.hAlert.text = "";
		}
		if(mstats.left_btn && plrHudMessage.txt != "" && plrHudMessage.alpha >= 1.0) 
		{
			plrHudMessage.txt = "";
			mstats.left_btn = false;
		}
							
		if(plrHudMessage.txt != "" && plrHudMessage.alpha > 0) 
		{
			vector2 messagepos = (960, 450);
			H_DrawTexture(tMessageBox,(messagepos.x+30,messagepos.y),0,(1.3,0.7),plrHudMessage.alpha);
			H_DrawString(plrHudMessage.txt,(messagepos.x-240,messagepos.y-90),0,plrHudMessage.col,plrHudMessage.alpha);
			H_DrawString("Click to continue",(messagepos.x-116,messagepos.y-24),0,Font.CR_White,plrHudMessage.alpha);
			if(plrHudMessage.alpha < 1.0) plrHudMessage.alpha += 0.05;
		}
		
	}
	
	override void Ticker()
	{
		if(inputclear == 0)
		{
			mstats.left_pressed = false;
			mstats.right_pressed = false;
			mstats.scroll = 0;
			inputclear = 12;
		}
		if(inputclear > 0) inputclear --;
	}
}


