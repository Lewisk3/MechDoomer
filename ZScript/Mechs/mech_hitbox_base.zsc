class MechHitbox : Actor
{
	MechHardpoint mounted_hp;
	Mechwarrior mech;
	double xoffs, yoffs, forward;
	
	Default
	{
		Health int.max;
		+SHOOTABLE;
		+NOGRAVITY;
		+NOBLOOD;
		Radius 5;
		Height 5;
	}
		
	static MechHitBox SetupHitbox(Mechwarrior basemech, MechHardpoint mountpoint)
	{
		let box = MechHitBox(Actor.Spawn("MechHitbox", basemech.pos));
		if(box)
		{
			box.A_SetSize(mountpoint.hitbox_info.radius, mountpoint.hitbox_info.height);
			box.xoffs = mountpoint.hitbox_info.xoffs;
			box.yoffs = mountpoint.hitbox_info.yoffs;
			box.forward = mountpoint.hitbox_info.forward;
			box.mech = basemech;
			box.mounted_hp = mountpoint;
		}
		return box;
	}
	 
	override int DamageMobJ(Actor inflictor, Actor source, int damage, Name mod, int flags, double angle)
	{
		// Take no damage directly, instead redirect to hardpoint.
		if(!mech || mech.checkVoodoo() || !mounted_hp || Mechwarrior(inflictor) == mech || Mechwarrior(source) == mech) return 0;
		Actor damager = inflictor;
		let mechweap = Mechbase_Actor(inflictor);
		if(mod == "hitscan") damager = source;
		double realdamage = mechweap ? mechweap.truedamage : damage;
		bool mechdmg = (inflictor is "Mechwarrior") || (source is "Mechwarrior");
		double newdmg = mechdmg ? (realdamage*0.5) : (realdamage*MWGAMEPLAY_DAMAGESCALE);
		
		// Take a bit less damage from AI
		if(mechdmg && !Mechwarrior(source).player) newdmg *= 0.4;
		
		// Take more damage if we are AI
		if(!mech.player) newdmg *= 1.35;
		
		mech.DamageHardpoint(mounted_hp, newdmg, false, damager);
		return 0;
	}
	
	bool CheckTorsoMount()
	{
		return (mounted_hp != mech.hardpoints.ll) && (mounted_hp != mech.hardpoints.rl);
	}
	
	override void Tick()
	{
		super.Tick();
		double truecockpit = mech.angle;
		if(!mech || !mech.models_reference) 
		{
			Destroy();
			return;
		}
		if(mech.models_reference.cockpit && mech.player) 
			truecockpit = mech.models_reference.cockpit.angle;

		if(!mounted_hp) return;
		double offsangle = CheckTorsoMount() ? truecockpit : mech.LegAng;
		double offspitch = CheckTorsoMount() ? mech.models_reference.roll*-MDMath.Sign(xoffs) : 0;
		vector3 offs = MDMath.VOffsetPitch(offsangle,offspitch, forward, xoffs, 1);	
		Vector3 finalpos = mech.Vec3Offset(offs.x,offs.y,yoffs+(offs.z*18));
		SetOrigin(finalpos, false);
	}
	
	States
	{
		Spawn:
			TNT1 A 1;
		loop;
	}
}
