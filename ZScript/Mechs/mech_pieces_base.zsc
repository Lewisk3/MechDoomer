// Camera Drone
class MechCamera : Actor
{
	MechWarriorBase mechowner;
	vector3 followpos;
	float followtilt;
	float followdist;
	float followdist_desired;
	float followdist_max;
	float rotangle;
	float rotspd;
	bool frontcam;
	
	Property FollowDistance : followdist;
	
	Default
	{
		+NOGRAVITY;
		Radius 1;
		Height 1;
		MechCamera.FollowDistance 5;
	}
		
	static MechCamera Create(MechWarriorBase torso, float dist, float angulardist, float rotation, bool frontfacing=false)
	{
		let cam = MechCamera(Spawn("MechCamera"));
		if(cam) 
		{
			cam.mechowner = torso;
			cam.followdist_desired = dist;
			cam.followdist_max = dist;
			cam.rotspd = rotation;
			cam.frontcam = frontfacing;
			cam.followtilt = angulardist;
			torso.player.camera = cam;
		}
		return cam;
	}
	
	vector3 GetFollowPos()
	{
		float zoffs = (mechowner.viewheight)+20;
		float trueangle = !rotangle ? mechowner.angle : rotangle;
		float truetilt = frontcam ? 0 : followtilt;
		
		float dmul = followdist*(frontcam ? -1 : 1);
		FLineTraceData lndata;
		mechowner.LineTrace(angle+truetilt+180,followdist_max,0,TRF_THRUACTORS|TRF_THRUBLOCK|TRF_ABSOFFSET,zoffs+sin(mechowner.pitch),0,0,lndata);
		if( lndata.HitType == lndata.TRACE_HitWall    || 
		    lndata.HitType == lndata.TRACE_HitCeiling || 
			lndata.HitType == TRACE_HitFloor			  ) 
		{
			followdist_desired = lndata.Distance;
			if(followdist > followdist_desired) followdist = followdist_desired;
		}
		else
		{	
			followdist_desired = followdist_max;
		}
		
		// Backwards interpolate from player center to new pos.
		// This makes clipping out of walls a bit less noticable
		// because, instead of going from inside the wall in, we're going
		// from inside the player out to the wall.
			
		vector3 origpos = mechowner.Vec3Offset(
			-cos(trueangle+truetilt),
			-sin(trueangle+truetilt),
			zoffs+sin(mechowner.pitch)
		);
		vector3 newfollowpos = mechowner.Vec3Offset(
			-cos(trueangle+truetilt)*dmul,
			-sin(trueangle+truetilt)*dmul,
			zoffs+sin(mechowner.pitch)*dmul
		);
		if(!followpos.length()) followpos = newfollowpos;
		newfollowpos = MDMath.Lerp3D(origpos, newfollowpos, 0.6);
		return newfollowpos;
	}
	
	override void tick()
	{
		if(mechowner)
		{
			if(followdist < followdist_desired && mechowner.dronecam_active) 
				followdist = MDMath.Lerp(followdist,followdist_desired,0.05);
				
			float lerpspd = 0.15;
			rotangle += rotspd;
			vector3 newfollowpos = GetFollowPos();
			
			if(!mechowner.dronecam_active)
			{
				if(Distance2D(mechowner) <= 24) Destroy();
				followdist = MDMath.Lerp(followdist,0,0.10);
				if(followdist <= 10) lerpspd=1;
			}
			followpos = MDMath.Lerp3D(followpos, newfollowpos, lerpspd);
			angle = AngleTo(mechowner)-(frontcam ? 0 : followtilt);
			pitch = MDMath.getPitchBetween(pos,(mechowner.pos.x,mechowner.pos.y,mechowner.pos.z+mechowner.viewheight+(frontcam ? 0 : 20)));
			SetOrigin(followpos,true);
		}
	}	
	
	States
	{
		Spawn:
			MCAM A 1;
		loop;
	}
}

// Parts base
class MechPartsBase : Actor
{
	MechwarriorBase mechowner;
	float zoffs;
	float cxoffs;
	float czoffs;
	Property ZOffset : zoffs;
	Property CockpitOffset : cxoffs, czoffs;
	
	Default
	{
		+NOGRAVITY;
		MechPartsBase.ZOffset 0;
		MechPartsBase.CockpitOffset 0, 60;
	}
	
	virtual vector3 getMechPos()
	{
		vector3 basepos = mechowner.Vec3Offset(0,0,zoffs);
		return basepos;
	}
	
	override void tick()
	{
		super.Tick();
		if(mechowner) SetOrigin(getMechPos(),true);
	}	
	
	static MechPartsBase Create(class<MechPartsBase> part, MechWarriorBase base)
	{
		let mwpart = MechPartsBase(Spawn(part));
		if(mwpart)
		{
			mwpart.mechowner = base;
			mwpart.frame = 0;
		}
		return mwpart;
	}
	States
	{
		Spawn:
			MODL A 1;
		loop;
	}
}

// Cockpit cameras
class MechCockpitCamera : MechPartsBase
{
	Default
	{
		+NOINTERACTION;
	}

	float defz;	
	float cur_viewz;
	float rot_spd;
	
	override vector3 getMechPos()
	{
		float distoffs = cxoffs;
		FLineTraceData lndata;
		mechowner.LineTrace(angle,cxoffs,0,TRF_THRUACTORS|TRF_THRUBLOCK|TRF_ABSOFFSET,czoffs-sin(mechowner.pitch),0,0,lndata);
		if( lndata.HitType == lndata.TRACE_HitWall    || 
		    lndata.HitType == lndata.TRACE_HitCeiling || 
			lndata.HitType == TRACE_HitFloor			  ) 
		{
			distoffs = lndata.Distance-20;
		}
		if(mechowner.pos.z+czoffs+20 > ceilingz) 
		{
			float diff = (mechowner.pos.z+czoffs+20)-ceilingz;
			czoffs -= diff;
		}
		vector3 basepos = mechowner.Vec3Offset(cos(mechowner.angle)*distoffs,sin(mechowner.angle)*distoffs,czoffs);
		return basepos;
	}

	override void tick()
	{
		if(!defz) defz = czoffs;
		if(mechowner && !mechowner.CheckVoodoo()) 
		{		
			float fovdiff = mechowner.player.FOV/mechowner.defaultfov*0.5;
			CameraFOV = mechowner.player.FOV;
			angle += mechowner.LegDelta;

			angle = MDMath.Lerp(angle,mechowner.angle,0.6*fovdiff);
			pitch = MDMath.Lerp(pitch,mechowner.pitch+(mechowner.viewzoffs*0.065),0.6*fovdiff);
			roll = mechowner.roll;
			czoffs = defz+(mechowner.viewzoffs);
		}
		super.tick();
	}
}
class MechCockpitModel : MechPartsBase
{
	float mxoffs;
	float mzoffs;
	float maxpitch;
	float maxangle;
	float rotatespeed;
	float bobrange;
	Property Offset: mxoffs, mzoffs;
	MechCockpitCamera cockpitcam;	
	
	Default
	{
		+INTERPOLATEANGLES
		+NOINTERACTION
	}
	
	override vector3 getMechPos()
	{
		float fxoffs = cockpitcam.cxoffs+mxoffs;
		float fzoffs = cockpitcam.czoffs+mzoffs;
		FLineTraceData lndata;
		mechowner.LineTrace(angle,fxoffs,0,TRF_THRUACTORS|TRF_THRUBLOCK|TRF_ABSOFFSET,fzoffs-sin(mechowner.pitch),0,0,lndata);
		if( lndata.HitType == lndata.TRACE_HitWall    || 
		    lndata.HitType == lndata.TRACE_HitCeiling || 
			lndata.HitType == TRACE_HitFloor			  ) 
		{
			fxoffs = lndata.Distance-20;
		}
		if(mechowner.pos.z+fzoffs+20 > ceilingz) 
		{
			float diff = (mechowner.pos.z+fzoffs+20)-ceilingz;
			fzoffs -= diff;
		}
		vector3 basepos = mechowner.Vec3Offset(cos(mechowner.angle)*fxoffs,sin(mechowner.angle)*fxoffs,fzoffs);
		return basepos;
	}
	
	override void tick()
	{
		if(!cockpitcam)
		{
			Destroy();
			return;
		}
		cockpitcam.rot_spd = rotatespeed;	
		
		if(mechowner && !mechowner.CheckVoodoo()) 
		{	
			if( (!mechowner.mech_shutdown && !mechowner.pilotlook) || mechowner.defaultmovement )
			{
				if(!mechowner.defaultmovement && GetAge() > 2)
				{					
					float toangle = mechowner.angle;
					if(toangle - mechowner.LegAng >  maxangle) toangle = mechowner.LegAng + maxangle;
					if(toangle - mechowner.LegAng < -maxangle) toangle = mechowner.LegAng - maxangle;
					float topitch = clamp(mechowner.pitch, -maxpitch, maxpitch);
					angle += mechowner.LegDelta;
					
					// float rot_tic = rotatespeed/35.;
					// A_SetAngle(MDMath.Strp(angle,toangle, rot_tic),SPF_INTERPOLATE);
					float old_pitch = pitch;
					float old_angle = angle;
					A_SetAngle(MDMath.Lerp(angle,toangle, rotatespeed*0.6),SPF_INTERPOLATE);
					A_SetPitch(MDMath.Lerp(pitch,topitch+(mechowner.viewzoffs*0.75),0.1),SPF_INTERPOLATE);
					A_SetRoll (mechowner.roll,SPF_INTERPOLATE);
					
					// Torso rotation sounds.
					if( abs(old_pitch-pitch) > (rotatespeed*0.6) || 
					    abs(old_angle-angle) > 0.1					) 
						A_StartSound("Mech/TorsoTwist",CHAN_BODY,0,1,0.3); 
					else 
						A_StopSound(CHAN_BODY); 
				}
				else
				{
					pitch = mechowner.pitch;
					angle = mechowner.angle;
					roll = mechowner.roll;
				}
			}
			mzoffs = (mechowner.viewzoffs*0.12)*bobrange;
		}
		
		if(!mechowner.dronecam && !mechowner.CheckVoodoo()) 
			bInvisible = false;
		else 
			bInvisible = true;
		
		// bInvisible = false; // Debug
		super.Tick();
	}
}

// Torsos
class MechTorsoBase : MechPartsBase
{
	bool notwist;
	bool setup;
	float cockpitpitch_mod;
	float cockpitangle_mod;
	float cockpittwist_mod;
	float cockpitbob_mod;
	MechPartsBase hips;
	MechPartsBase legs;
	MechPartsbase cockpit_camera;
	MechCockpitModel cockpit;
	class<MechPartsBase> hips_type;
	class<MechPartsBase> legs_type;
	class<MechCockpitModel> cockpit_type;
	Property Pieces : hips_type, legs_type, cockpit_type;
	Property CockpitRange : cockpitangle_mod, cockpitpitch_mod, cockpitbob_mod;
	Property TwistSpeed : cockpittwist_mod;
	Property DoNotTwist : notwist;
	
	Default
	{
		MechTorsoBase.CockpitRange 0.7, 0.7, 1.0;
		MechTorsoBase.TwistSpeed 0.1;
	}
		
	void SetupParts(class<MechPartsBase> phips, class<MechPartsBase> plegs, class<MechCockpitModel> cpit)
	{
		cockpit_camera = MechPartsBase.Create("MechCockpitCamera", mechowner);
		if(phips) hips = MechPartsBase.Create(phips, mechowner);
		if(plegs) legs = MechPartsBase.Create(plegs, mechowner);
		if(hips) hips.zoffs = zoffs;
		if(cockpit_camera) 
		{
			cockpit_camera.cxoffs = cxoffs;
			cockpit_camera.czoffs = czoffs;
		}
		if(cpit) 
		{
			cockpit = MechCockpitModel(MechPartsBase.Create(cpit, mechowner));
			if(cockpit) 
			{
				cockpit.cockpitcam = MechCockpitCamera(cockpit_camera);
				cockpit.angle = mechowner.angle;
				cockpit.maxpitch = mechowner.maxPitch * cockpitpitch_mod;
				cockpit.maxangle = mechowner.MaxYaw * cockpitangle_mod;
				cockpit.rotatespeed = cockpittwist_mod;
				cockpit.bobrange = cockpitbob_mod;
			}
		}
		setup = true;
	}
	
	override void tick()
	{
		if(!setup) SetupParts(hips_type, legs_type, cockpit_type);
		if(mechowner)
		{
			if( !mechowner.dronecam && !mechowner.dronecam_active &&
				 mechowner.player && mechowner.player.camera == mechowner ) 
				mechowner.player.camera = cockpit_camera;
			
			if(!notwist)
			{
				pitch = mechowner.pitch*0.5;
				roll = mechowner.roll*0.3;
				angle = cockpit.angle;
			}
			else
			{
				angle = mechowner.LegAng;
			}
			if(!mechowner.dronecam && !mechowner.CheckVoodoo()) 
				bInvisible = true;
			else
				bInvisible = false;
		}
		super.tick();
	}
	override void OnDestroy()
	{
		if(hips) hips.Destroy();
		if(legs) legs.Destroy();
		if(cockpit_camera) cockpit_camera.Destroy();
	}
	States
	{
		Spawn:
			TRSO A 1;
		loop;
	}
}

// Hips
class MechHipsBase : MechPartsBase
{
	override void tick()
	{
		if(mechowner)
		{
			angle = mechowner.LegAng;
			if(!mechowner.dronecam && !mechowner.CheckVoodoo()) 
				bInvisible = true;
			else
				bInvisible = false;
		}
		super.tick();
	}
}

// Legs
class MechLegsBase : MechPartsBase
{
	float spdrating;
	float stompfriction;
	float runspeed;
	
	Property RunSpeed : runspeed;
	
	Default
	{
		MechLegsBase.RunSpeed 0.5;
	}
	
	override void tick()
	{
		angle = mechowner.LegAng;
		if(mechowner && !mechowner.CheckVoodoo())
		{
			if((!mechowner.dronecam && mechowner.nofirstpersonlegs)) 
				bInvisible = true;
			else
				bInvisible = false;
				
			float truemove = !mechowner.MoveSpeedKPH ? 1 : mechowner.MoveSpeedKPH; 
			float curspd = abs(mechowner.LegVel*0.5)+(abs(mechowner.move_throttle) * truemove);
			float wrating = float(mechowner.inv_context.weapons_tonnage)/100.;
			float stomp_spdrating = (curspd / truemove)*0.3;
			float stomp_wrating = 1.0-(0.7*wrating);
			
			spdrating = curspd / (truemove*runspeed);
			stompfriction = stomp_wrating + stomp_spdrating;
		}
		super.tick();
	}
	bool isMoving()
	{
		return ( 
			 mechowner && !mechowner.checkVoodoo() && !mechowner.mech_shutdown && 
		    (mechowner.player && mechowner.player.onground) && 
			(mechowner.vel.xy.length() >= 0.08 || mechowner.LegVel) && 
			!mechowner.isdead
		);
	}
	virtual bool checkStepping(bool idle=false, int frameone=3, int frametwo=8)
	{
		if(!mechowner) return false;
		mechowner.stepping = idle ? false : (frame == frameone || frame == frametwo);
		if(mechowner.stepping) mechowner.A_AlertMonsters(1024);
		return true;
	}
	
	States
	{
		Spawn:
			TLEG A 1;
		SpawnIdle:
			"####" "#" 1 A_JumpIf(checkStepping(true) && isMoving(), "WalkCycle");
		loop;
		WalkCycle:
			TLEG BCDEFGHI 4 
			{	
				if( (frame == 2 || frame == 7) ) SoundEmitter.SE_Trigger("Mech/Footstep",mechowner.pos);
				if(checkStepping() && mechowner.stepping) mechowner.vel.xy *= stompfriction;	
				if(spdrating > 0) tics = clamp(10-(abs(spdrating)*10.), 4, 10);
				return !isMoving() ? ResolveState("SpawnIdle") : (tics <= 5 && frame >= 7) ? ResolveState("RunCycle") : ResolveState(Null); 
			}
		loop;
		RunCycle:
			TLEG KLMNOPQRS 4 
			{	
				if( (frame == 10 || frame == 15) ) SoundEmitter.SE_Trigger("Mech/Footstep",mechowner.pos);
				if(checkStepping(false, 11, 16) && mechowner.stepping) mechowner.vel.xy *= stompfriction;	
				if(spdrating > 0) tics = clamp(10-(abs(spdrating)*10.), 3, 10);
				return !isMoving() ? ResolveState("WalkCycle") : (tics > 5 && frame <= 10) ? ResolveState("WalkCycle") : ResolveState(Null); 
			}
		loop;
	}
}

class Legs_Smooth  : MechLegsBase 
{
	States
	{
		Spawn:
			TLE1 A 1;
		SpawnIdle:
			"####" "#" 1 A_JumpIf(checkStepping(true) && isMoving(), "WalkCycle");
		loop;
		WalkCycle:
			TLE1 BCDEFGHIJKLMNOPQRSTUVWXYZ 2 
			{	
				if(frame == 1 || frame == 15) SoundEmitter.SE_Trigger("Mech/Footstep",mechowner.pos);
				if(checkStepping(false, 2, 16) && mechowner.stepping) mechowner.vel.xy *= stompfriction;	
				if(spdrating > 0) tics = clamp(10-(abs(spdrating)*10.), 1, 2);
				return !isMoving() ? ResolveState("SpawnIdle") : ResolveState(Null);
			}
			TLE2 ABCDEFG 2
			{
				if(spdrating > 0) tics = clamp(10-(abs(spdrating)*10.), 1, 2);
				return !isMoving() ? ResolveState("SpawnIdle") : (tics <= 1 && frame > 5) ? ResolveState("RunCycle") : ResolveState(Null);
			}
		loop;
		RunCycle:
			TLE3 BCDEFGHIJKLMNOPQRSTUVWXYZ 2 
			{	
				if(frame == 7 || frame == 22) SoundEmitter.SE_Trigger("Mech/Footstep",mechowner.pos);
				if(checkStepping(false, 8, 23) && mechowner.stepping) mechowner.vel.xy *= stompfriction;	
				if(spdrating > 0) tics = clamp(10-(abs(spdrating)*10.), 1, 3);
				return !isMoving() ? ResolveState("SpawnIdle") : (tics >= 3 && frame == 1) ? ResolveState("WalkCycle") : ResolveState(Null);
			}
			TLE4 ABCDEF 2
			{
				if(spdrating > 0) tics = clamp(10-(abs(spdrating)*10.), 1, 3);
			}
		loop;
	}
}