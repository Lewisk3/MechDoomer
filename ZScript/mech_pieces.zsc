class MechLegsBase : Actor
{
	MechwarriorBase mechowner;
	
	override void tick()
	{
		if(mechowner)
		{
			angle = mechowner.LegAng;
			SetOrigin(mechowner.pos,true);
		}
		super.tick();
	}
	
	bool isMoving()
	{
		return (mechowner && (mechowner.vel.xy.length() >= 0.08 || mechowner.LegVel) );
	}
	
	static MechLegsBase Create(class<MechLegsBase> legs, MechWarriorBase torso)
	{
		let mwlegs = MechLegsBase(Spawn(legs));
		if(mwlegs)
		{
			mwlegs.mechowner = torso;
			mwlegs.frame = 0;
		}
		return mwlegs;
	}
	
	States
	{
		Spawn:
			TLEG "#" 1 A_JumpIf(isMoving(), "WalkCycle");
		loop;
		WalkCycle:
			TLEG BCDEFGHIJ 4 
			{
				if(frame == 4 || frame == 8) SoundEmitter.SE_Trigger("Mech/Footstep",mechowner.pos);
				
				//float spdrating = (mechowner.throttle * mechowner.MoveSpeedKPH)/100.;
				//if(spdrating > 0) tics = clamp(10-(abs(spdrating)*10.), 2, 10);
				return !isMoving() ? ResolveState("Spawn") : ResolveState(Null); 
			}
		loop;
	}
}


class Legs_Timberwolf : MechLegsBase {}
class Legs_Stormcrow : MechLegsBase {}
