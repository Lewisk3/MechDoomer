
// Cockpit/crosshair handler(s)
class CockpitHandler : Weapon
{
	int bobdir;
	
	Default
	{
		Weapon.SlotNumber 6;
		Weapon.SelectionOrder 3700;
		+WEAPON.AMMO_OPTIONAL;
		Tag " ";
	}

	override void PostBeginPlay()
	{
		bobdir = 1;
		super.PostBeginPlay();
	}
	
	action MechWarrior getPilot()
	{
		let plr = MechWarrior(Self);
		return plr;
	}
	
	
	action PSprite getPSprite()
	{
		let plr = getPilot();
		if(!plr) return NULL;
		return plr.player.GetPSprite(PSP_WEAPON);
	}
	
	States
	{			
		Ready:
			TNT1 A 0;
			MODL A 1 Fast 
			{
				let plr = getPilot();
				if(plr) 
				{	
					// Footsteps when bob is at adequite length.
					if(!plr.mech_models && plr.bobdir != invoker.bobdir && plr.vel.length() >= 0.08 )
					{
						if(plr.bobdir == 1) A_FireProjectile("MechFootstepEmitter");
						invoker.bobdir = plr.bobdir;
					}
				
					// Model bob
					float weapony = getPSprite().y;
					float xoffs = -plr.rolldistort;
					float yoffs = MDMath.lerp(weapony,32.+plr.boboffs*0.8,0.3);
					
					if( (plr.mech_shutdown || plr.pilotlook) && !plr.defaultmovement) 
					{
						xoffs = (plr.LegAng-angle)*2.47;
						yoffs += -(pitch-sin(90));
					}
					else
					{
						if(!plr.uncappedtorso && !plr.defaultmovement) xoffs += (plr.LegAng-angle)*0.04;
						yoffs += pitch*0.12;
					}
					A_WeaponOffset(xoffs,yoffs,WOF_INTERPOLATE);
					
					if(!plr.mech_shutdown)
					{ 
						let weap = plr.getSelectedWeapon();
						if(weap) 
						{
							if(weap.isHomming) 
								A_SetCrossHair(plr.targetlocked ? 12 : plr.locktimer > 25 ? 11 : 10);
							else
								A_SetCrossHair( (weap.cooldown_timer || plr.player.cmd.buttons & BT_ATTACK) ? 9 : 8 ); 
						}
					}
					if(CVar.FindCVar("mwd_nohud").getBool() || plr.mech_shutdown) A_SetCrossHair(0);
				}
				A_WeaponReady(WRF_NOBOB);
			}
		loop;
		Select:
			TNT1 A 0;
			MODL A 1 Offset(-1,35);
		DoSelect:
			TNT1 A 0 A_Raise();
		loop;
		Deselect:
			TNT1 A 0;
			MODL A 1 Offset(1,300);
		DoLower:
			CROW A 0 A_Lower();
		loop;
		Fire:
			MODL A 1;
		goto Ready;	
	}
}

// Registered Cockpits
class CockpitHandler_Timberwolf : CockpitHandler {}
class CockpitHandler_Stormcrow : CockpitHandler {}
class CockpitHandler_Jenner : CockpitHandler {}
class CockpitHandler_Atlas : CockpitHandler {}
class CockpitHandler_Commando : CockpitHandler {}
class CockpitHandler_Urbanmech : CockpitHandler {}
class CockpitHandler_BattleArmor : CockpitHandler {}
class CockpitHandler_Nova : CockpitHandler {}
