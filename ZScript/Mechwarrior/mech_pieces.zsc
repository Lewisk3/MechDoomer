// Camera Drone
class MechCamera : Actor
{
	MechWarriorBase mechowner;
	vector3 followpos;
	float followdist;
	float followdist_desired;
	float followdist_max;
	float rotangle;
	float rotspd;
	bool frontcam;
	
	Property FollowDistance : followdist;
	
	Default
	{
		+NOGRAVITY;
		Radius 1;
		Height 1;
		MechCamera.FollowDistance 50;
	}
		
	static MechCamera Create(MechWarriorBase torso, float dist, float rotation, bool frontfacing=false)
	{
		let cam = MechCamera(Spawn("MechCamera"));
		if(cam) 
		{
			cam.mechowner = torso;
			cam.followdist_desired = dist;
			cam.followdist_max = dist;
			cam.rotspd = rotation;
			cam.frontcam = frontfacing;
			torso.player.camera = cam;
		}
		return cam;
	}
	
	vector3 GetFollowPos()
	{
		if(!mechowner) return (0,0,0);
		FLineTraceData lndata;
		float zoffs = (mechowner.viewheight);
		float trueangle = !rotangle ? mechowner.angle : rotangle;
		float dmul = followdist*(frontcam ? -1 : 1);
		vector3 newfollowpos = mechowner.Vec3Offset(
			-cos(trueangle),
			-sin(trueangle),
			zoffs+sin(mechowner.pitch)
		);
		if(!followpos.length()) followpos = newfollowpos;
		mechowner.LineTrace(angle+180,followdist_max,0,TRF_THRUACTORS|TRF_THRUBLOCK|TRF_ABSOFFSET,zoffs+sin(mechowner.pitch),0,0,lndata);
		if( lndata.HitType == lndata.TRACE_HitWall    || 
		    lndata.HitType == lndata.TRACE_HitCeiling || 
			lndata.HitType == TRACE_HitFloor			  ) 
		{
			followdist_desired = lndata.Distance;
			if(followdist > followdist_desired) followdist = followdist_desired;
		}
		else
		{	
			followdist_desired = followdist_max;
		}
		// For some dumb reason i can't do a vec3*=dmul here, doesn't work right.
		vector3 corrpos = mechowner.Vec3Offset(
			-cos(trueangle)*dmul,
			-sin(trueangle)*dmul,
			zoffs+sin(mechowner.pitch)*dmul
		);
		newfollowpos = MDMath.Lerp3D(newfollowpos, corrpos, 0.6);
		return newfollowpos;
	}
	
	override void tick()
	{
		if(mechowner)
		{
			if(followdist < followdist_desired && mechowner.dronecam_active) 
				followdist = MDMath.Lerp(followdist,followdist_desired,0.05);
				
			float lerpspd = 0.15;
			rotangle += rotspd;
			vector3 newfollowpos = GetFollowPos();
			
			if(!mechowner.dronecam_active)
			{
				if(Distance2D(mechowner) <= 24) Destroy();
				followdist = MDMath.Lerp(followdist,0,0.10);
				if(followdist <= 10) lerpspd=1;
			}
			followpos = MDMath.Lerp3D(followpos, newfollowpos, lerpspd);
			angle = AngleTo(mechowner);
			pitch = MDMath.getPitchBetween(pos,(mechowner.pos.x,mechowner.pos.y,mechowner.pos.z+mechowner.viewheight));
			SetOrigin(followpos,true);
		}
	}	
	
	States
	{
		Spawn:
			MCAM A 1;
		loop;
	}
}


// Legs
class MechLegsBase : Actor
{
	MechwarriorBase mechowner;
	
	Default
	{
		+NOGRAVITY;
	}
	
	override void tick()
	{
		if(mechowner)
		{
			angle = mechowner.LegAng;
			SetOrigin(mechowner.pos,true);
			if(!mechowner.dronecam && mechowner.nofirstpersonlegs) 
				Sprite = GetSpriteIndex("TNT1A0");
			else
				Sprite = GetSpriteIndex("TLEGA0");
		}
		super.tick();
	}
	
	bool isMoving()
	{
		return ( 
			 mechowner && !mechowner.checkVoodoo() && !mechowner.mech_shutdown && 
		    (mechowner.player && mechowner.player.onground) && 
			(mechowner.vel.xy.length() >= 0.08 || mechowner.LegVel) && 
			!mechowner.isdead
		);
	}
	bool checkStepping(bool idle=false)
	{
		if(!mechowner) return false;
		mechowner.stepping = idle ? false : (frame == 3 || frame == 8);
		return true;
	}
	
	static MechLegsBase Create(class<MechLegsBase> legs, MechWarriorBase torso)
	{
		let mwlegs = MechLegsBase(Spawn(legs));
		if(mwlegs)
		{
			mwlegs.mechowner = torso;
			mwlegs.frame = 0;
		}
		return mwlegs;
	}
	
	States
	{
		Spawn:
			TLEG A 1;
		SpawnIdle:
			"####" "#" 1 A_JumpIf(checkStepping(true) && isMoving(), "WalkCycle");
		loop;
		WalkCycle:
			TLEG BCDEFGHIJ 4 
			{	
				float spdrating = (mechowner.throttle * mechowner.MoveSpeedKPH)/160.;
				float wrating = float(mechowner.inv_context.weapons_tonnage)/100.;
				float stompfriction = clamp((0.77) + (wrating*(spdrating)), 0.87, 1.05);
				if(checkStepping() && mechowner.stepping) mechowner.vel.xy *= stompfriction;
				if( (frame == 2 || frame == 7) ) SoundEmitter.SE_Trigger("Mech/Footstep",mechowner.pos);
				if(spdrating > 0) tics = clamp(10-(abs(spdrating)*10.), 3, 10);
				// console.printf("--------\nSR: %f\nWR: %f\nFR: %f", spdrating, wrating, stompfriction);
				return !isMoving() ? ResolveState("SpawnIdle") : ResolveState(Null); 
			}
		loop;
	}
}


class Legs_Timberwolf : MechLegsBase {}
class Legs_Stormcrow : MechLegsBase {}
class Legs_Atlas : MechLegsBase {}
class Legs_Commando : MechLegsBase {}
class Legs_Urbanmech : MechLegsBase {}
class Legs_Jenner : MechLegsBase {}
class Legs_Invisible : MechLegsBase {}
