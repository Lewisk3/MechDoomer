// Base
class MechutilDefs
{
	int hardslots;
	String displayName;
	float weight;
	bool moveable;
	bool takesnodamage;
	int worth;
}

class Mechbase_Utility : Actor
{
	enum Hardpoints
	{
		Head = 0,
		CTorso,
		LTorso,
		RTorso,
		LArm,
		RArm,
		LLeg,
		RLeg
	}

	int hardslots;
	String displayName;
	float weight;
	bool moveable;
	bool takesnodamage;
	int worth;
	
	Property Criticals : hardslots;
	Property DisplayName : displayName;
	Property CValue : worth;
	Property Tonnage : weight;
	Property Moveable : moveable;
	Property DiamondCasing : takesnodamage; 
	
	Default
	{
		Mechbase_Utility.Tonnage 0;
		Mechbase_Utility.Moveable 0;
		Mechbase_Utility.CValue 0;
	}
	
	clearscope static string FormatFloat(float num, int pushback=3)
	{
		String snum = num.."";
		snum.truncate(snum.Length()-pushback);	
		return snum;
	}
	
	static Mechbase_Utility CreateTemp(class<Mechbase_Utility> type="Mechbase_Utility")
	{
		let mbu = Mechbase_Utility(Spawn(type,level.PickPlayerStart(0)));
		return mbu;
	}
	
	clearscope static MechutilDefs getDefaults(class<Mechbase_Utility> type="Mechbase_Utility")
	{
		let defs = GetDefaultByType(type);
		let mu = new("MechutilDefs");
		mu.hardslots = defs.hardslots;
		mu.displayName = defs.displayName;
		mu.worth = defs.worth;
		mu.weight = defs.weight;
		mu.moveable = defs.moveable;
		mu.takesnodamage = defs.takesnodamage;
		return mu;
	}
	
	void GetDefDescription(in out Array<String> desc)
	{
		desc.push("Name: " .. displayName);
		desc.push("Weight: " .. FormatFloat(weight) .. " tons");
		desc.push("Criticals: " .. hardslots);
		desc.push("CBill Value: " .. worth);
	}
	
	virtual void SetupItem(mw_InvStruct item)
	{
		item.utildefs = getDefaults(item.Tag);
		GetDefDescription(item.description);
	}
	
}

class Mechbase_Ammo : Mechbase_Utility
{
	int ammogive;
	property Amount : ammogive;
	
	Default
	{
		Mechbase_Ammo.Amount 1;
		Mechbase_Utility.Moveable 1;
	}
	
	override void SetupItem(mw_InvStruct item)
	{
		item.ammunition = ammogive;
		GetDefDescription(item.description);
	}
}

// Immovable 
class StructEngine : Mechbase_Utility
{
	Default
	{	
		Mechbase_Utility.displayName "ENGINE";
		Mechbase_Utility.Criticals 1;
	}
	override void SetupItem(mw_InvStruct item) {}
}
class StructGyro : Mechbase_Utility
{
	float power;
	Property Effectiveness : power;
	Default
	{	
		Mechbase_Utility.displayName "Gyro";
		Mechbase_Utility.Criticals 1;
		StructGyro.Effectiveness 0.36;
		Mechbase_Utility.Tonnage 1;
	}
}
class StructLifesupport : Mechbase_Utility
{
	Default
	{	
		Mechbase_Utility.displayName "LIFESUPPORT";
		Mechbase_Utility.Criticals 1;
	}
}
class StructSensors : Mechbase_Utility
{
	Default
	{	
		Mechbase_Utility.displayName "Sensors";
		Mechbase_Utility.Criticals 1;
	}
}
class StructCockpit : Mechbase_Utility
{
	Default
	{	
		Mechbase_Utility.displayName "Cockpit";
		Mechbase_Utility.Criticals 1;
		Mechbase_Utility.Tonnage 3.0;
	}
}
class StructShoulder : Mechbase_Utility
{
	Default
	{	
		Mechbase_Utility.displayName "Shoulder";
		Mechbase_Utility.Criticals 1;
	}
}
class StructHip : Mechbase_Utility
{
	Default
	{	
		Mechbase_Utility.displayName "Hip";
		Mechbase_Utility.Criticals 1;
	}
}
class StructActuator : Mechbase_Utility
{
	Default
	{	
		Mechbase_Utility.displayName "Actuator";
		Mechbase_Utility.Criticals 1;
	}
}

// Moveable
class C_A_S_E : Mechbase_Utility
{	
	Default
	{	
		Mechbase_Utility.Moveable True;
		Mechbase_Utility.displayName "C.A.S.E";
		Mechbase_Utility.Criticals 1;
		Mechbase_Utility.DiamondCasing True;
		Mechbase_Utility.Tonnage 0.5;
	}

	override void SetupItem(mw_InvStruct item)
	{
		GetDefDescription(item.description);
		item.description.push("Supresses ammo explosions.");
	}
}

class Heatsink : Mechbase_Utility
{
	float cooling;
	Property Efficiency : cooling;
	
	Default
	{
		Mechbase_Utility.Tonnage 1;
		Mechbase_Utility.displayName "Heat Sink";
		Heatsink.Efficiency MWHS_EFF;
		Mechbase_Utility.Criticals 1;
		Mechbase_Utility.Moveable 1;
		Mechbase_Utility.CValue 12600;
	}
	
	override void SetupItem(mw_InvStruct item)
	{
		String hef = cooling .. "";
		hef.truncate(hef.Length()-3);
		GetDefDescription(item.description);
		item.description.Insert(3,"Conductivity: +" .. hef);
	}
}
class DoubleHeatsink : Heatsink
{
	Default
	{
		Mechbase_Utility.Tonnage 1;
		Mechbase_Utility.displayName "Heat Sink";
		Heatsink.Efficiency MWDBLHS_EFF;
		Mechbase_Utility.Criticals 2;
		Mechbase_Utility.Moveable 1;
		Mechbase_Utility.CValue 22530;
	}
}

class JumpJet : Mechbase_Utility
{
	float thrust_power;
	Property thrust : thrust_power;
	
	Default
	{
		Mechbase_Utility.Tonnage 0.5;
		Mechbase_Utility.displayName "Jump Jet";
		Mechbase_Utility.Criticals 1;
		JumpJet.thrust MWJJET_THRUST;
		Mechbase_Utility.Moveable 1;
		Mechbase_Utility.CValue 8450;
	}
	
	override void SetupItem(mw_InvStruct item)
	{
		String thrust = ((thrust_power* 1/100) + 2) .. "";
		thrust.truncate(thrust.Length()-3);
		GetDefDescription(item.description);
		item.description.Insert(3,"Strength: " .. thrust);
	}
	
}

class AntiMissileSystem : Mechbase_Utility
{
	int cooldown;
	int pdamage;
	int ammouse;
	int activerange; // AMS becomes active when another missile enters range.
	class<AMS_Missile> amstype;
	class<Mechbase_Ammo> ammotype;
	
	Property Cooldown : cooldown;
	Property Damage : pdamage;
	Property AmmoUse : ammouse;
	property ActiveRange : activerange;
	Property MissileType : amstype;
	Property AmmoType : ammotype;

	Default
	{
		Mechbase_Utility.Tonnage 1.5;
		Mechbase_Utility.displayName "AMS";
		Mechbase_Utility.Criticals 2;
		Mechbase_Utility.Moveable 1;
		Mechbase_Utility.CValue 27050;
		AntiMissileSystem.Cooldown 3;
		AntiMissileSystem.Damage 15;
		AntiMissileSystem.ActiveRange 800;
		AntiMissileSystem.AmmoUse 1;
		AntiMissileSystem.AmmoType "AMSAmmo";
		AntiMissileSystem.MissileType "AntiMissile";
	}
	
}

// Armor
class ArmoryUtility : Inventory
{
	class<Inventory> type;
	int amt;
	float weight;
	
	Property Setup : type, amt;
	Property ArmorPointWeight : weight;
	
	Default
	{
		Inventory.maxamount 100; // 999
		+INVENTORY.ALWAYSPICKUP
	}
	
	
	override bool TryPickup (in out Actor toucher)
	{
		if(type)
		{
			toucher.GiveInventory(type,amt);
			Die(self,self);
			return false;
		}
		return super.TryPickup(toucher);
	}
	
	virtual void EquipUtil(MechwarriorBase plr, MechHardpoint hpoint)
	{
	
	}
	
}

class EndosteelBonus : ArmoryUtility replaces BasicArmorBonus
{
	Default
	{	
		Inventory.PickupMessage "Picked up a small Endosteel component";
		Tag "Endosteel Piece";
		Inventory.Icon "ENDOA0";
		XScale 0.3;
		YScale 0.3;
		ArmoryUtility.ArmorPointWeight 0.05;
	}
	States
	{
		Spawn:
			ENDO A 1;
		loop;
	}
	
	override void EquipUtil(MechwarriorBase plr, MechHardpoint hpoint)
	{
		/*
		if(hpoint.armor >= hpoint.max_armor) 
		{
			plr.sendHUDAlert("Hardpoint armor limit reached.",Font.CR_ORANGE);
			return;
		}
		*/
		if(hpoint.internalhp <= 0)
		{
			plr.sendHUDAlert("Hardpoint must be repaired first!",Font.CR_RED);
			return;
		}
		bool success = true;
		hpoint.armor++;
		if(hpoint.armor > hpoint.max_armor) success = plr.AdjustArmor(hpoint,1);
		if(success)
			plr.TakeInventory(getClassName(),1);
		else
		{
			plr.sendHUDAlert("Cannot add armor, over-tonnage!",Font.CR_RED);
			hpoint.armor--;
		}
	}
}
class EndoGreen : EndosteelBonus replaces GreenArmor 
{
	Default
	{
		ArmoryUtility.Setup "EndosteelBonus", 10;
		Inventory.PickupMessage "Picked up a medium Endosteel component";
	}

}
class EndoBlue : EndosteelBonus replaces BlueArmor 
{
	Default
	{
		ArmoryUtility.Setup "EndosteelBonus", 20;
		Inventory.PickupMessage "Picked up a large Endosteel component";
	}
}


// Misc (WIP) 
class Mechbay : Inventory
{
	Actor obj;

	Default
	{
		Radius 12;
		Height 4;
		-NOBLOCKMAP;
	}
	
	float point_distance(Actor one, Actor two, float scale)
	{
		int x = one.pos.x - two.pos.x;
		int y = one.pos.y - two.pos.y;
		int z = one.pos.z - two.pos.z;
		return sqrt( x*x + y*y + z*z )/scale;
	}
	
	override void Touch (Actor toucher)
	{
		if(!toucher.player) return;
		let plr = MechWarrior(toucher.player.mo);
		if(plr)obj = toucher;
	}
	
	void HealMechHardpoint(Mechwarrior plr, MechHardpoint hp)
	{
		if(!plr) return;
		if(hp.armor < hp.max_armor && plr.inv_context.money_cbills > 20) 
		{
			hp.armor+=1;
			plr.inv_context.money_cbills -= 20;
		}
		if(hp.internalhp < hp.max_internal && plr.inv_context.money_cbills > 20) 
		{
			hp.internalhp+=1;
			plr.inv_context.money_cbills -= 20;
		}
	}
	
	override void tick()
	{
		if(obj)
		{
			if(point_distance(obj.player.mo,self,1) > 50) 
			{
				obj = null;
				return;
			}
			let plr = MechWarrior(obj.player.mo);
			if(plr)
			{
				if(!plr.mech_shutdown)
				{
					plr.A_Print("Mech must be shutdown to initiate repairs.");
				}
				else
				{
					A_Print("Repairing...");
					if(plr.inv_context.money_cbills < 20){ plr.A_Print("Insufficient funds."); return; }
					
					// Heal mech hardpoints
					HealMechHardpoint(plr,plr.inv_context.hardpoints.hd);
					HealMechHardpoint(plr,plr.inv_context.hardpoints.ct);
					HealMechHardpoint(plr,plr.inv_context.hardpoints.lt);
					HealMechHardpoint(plr,plr.inv_context.hardpoints.rt);
					HealMechHardpoint(plr,plr.inv_context.hardpoints.la);
					HealMechHardpoint(plr,plr.inv_context.hardpoints.ra);
					HealMechHardpoint(plr,plr.inv_context.hardpoints.ll);
					HealMechHardpoint(plr,plr.inv_context.hardpoints.rl);	
				}
			}
		}
		super.tick();
	}
	
	override void PostBeginPlay()
	{
		float xo = (cos(angle+5))*8;
		float yo = (sin(angle+5))*8;
		//console.printf(": %f, %f",xoffset,yoffset);
		Vector3 lpos = (pos.x+xo,pos.y+yo,pos.z);
		let bay = Spawn("Mechbay_3D",pos);
		if(bay)
		{
			bay.target = Owner;
			bay.pitch = pitch;
			bay.angle = angle;
		}
		super.PostBeginPlay();
	}
	
	States
	{
		Spawn:
			MBAY A 1;
		loop;
	}
}
