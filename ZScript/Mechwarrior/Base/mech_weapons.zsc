	/*************************\
	|	  Weapons Systems     |
	\*************************/
extend class MechwarriorBase;

// Vars
Actor targetenemy;
Actor prevlock;
MechTargetReticle targetreticle;
bool weapon_groupfire;
bool targetlocked;
bool weapons_fired;
int selectedGroup;
int selectedWeapon;
double locktimer;

// Misc
void DestroyWeapon(Mechbase_Weapon weap)
{
	removeWeapon(weap);
	normalizeWeaponsID(1);
	Cockpit_Alert("\c[DARKRED]Weapon destroyed! ".. weap.displayName, 6.0);
	mechsounds.pushSound("Mech/Betty/WeaponDestroyed");
}

// Normalize
void normalizeWeaponsID(int offs)
{
	int weapsid = inv_context.weapons_count-offs;
	inv_context.weapons_count -= offs;
	for(let w = Inv; w; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww)
		{
			ww.WeaponID = weapsid;
			weapsid--;
		}
	}
}

// Get
int getWeaponSlotsUsed(Vector2 loc)
{
	int slots = 0;
	for(let w = Inv; w != null; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww)
		{
			if(ww.hardpoint.pos == loc) slots += ww.hardslots;
		}
	}
	return slots;
}
int getMaxWeaponGrouping()
{
	int mxgrp = 1;
	for(let w = Inv; w != null; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww)
		{
			if(ww.group > mxgrp)mxgrp = ww.group;
		}
	}
	return mxgrp;
}
Mechbase_Weapon getSelectedWeapon()
{	
	for(let w = Inv; w != null; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww)
		{
			if(ww.WeaponID == selectedWeapon) return ww;
		}
	}
	return null;
}
int getWeaponGroupAt(int ind)
{
	for(let w = Inv; w != null; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww)
		{
			if(ww.WeaponID == ind) return ww.group;
		}
	}
	return 1;
}
bool, int getNextWeaponIn(int ind, int grp)
{
	int mxgrp = 0;
	bool found = false;
	for(let w = Inv; w != null; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww)
		{
			if(ww.WeaponID > ind && ww.group == grp)
			{
				// /console.printf("Found: I: %d, G: %d",ww.WeaponID,ww.group);
				mxgrp = ww.WeaponID;
				found = true;
			}
		}
	}
	return found, mxgrp;
}
int getNearestWeaponAt(int group)
{
	int prevind = inv_context.weapons_count;
	for(let w = Inv; w != null; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww)
		{
			if( (ww.WeaponID < prevind) && ww.group == group )
			{
				prevind = ww.WeaponID;
			}
		}
	}
	return prevind;
}

// Set
bool setWeaponGroupAt(int ind, int group)
{
	for(let w = Inv; w != null; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww)
		{
			if(ww.WeaponID == ind) 
			{
				ww.group = group;
				return true;
			}
		}
	}
	return false;
}
void clearWeapons()
{
	ClearInventory();
	hardpoints.hd.slots = 0;
	hardpoints.ct.slots = 0;
	hardpoints.lt.slots = 0;
	hardpoints.rt.slots = 0;
	hardpoints.la.slots = 0;
	hardpoints.ra.slots = 0;
	hardpoints.ll.slots = 0;
	hardpoints.rl.slots = 0;
	inv_context.weapons_tonnage = 0;
	GiveLocalInv("WeaponsHandler",1);
	inv_context.weapons_count = 0;	
}

// Remove
void removeWeapon(Mechbase_Weapon w)
{
	// This is need because hardpoints and stuff.
	adjustTonnage(-w.weight);
	w.hardpoint.adjustHardslots(-w.hardslots);
	RemoveInventory(w);
}
bool removeWeaponAt(int ind)
{
	for(let w = Inv; w; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww)
		{
			if(ww.WeaponID == ind)
			{
				removeWeapon(ww);
				normalizeWeaponsID(1);
				return true;
			}
		}
	}
	return false;
}

// Swap or Move
bool moveWeaponToInv(int weap_ind)
{
	for(let w = Inv; w; w = w.Inv)
	{
		let ww = Mechbase_Weapon(w);
		if(ww)
		{
			if(ww.WeaponID == weap_ind)
			{
				addWeaponToInv(ww.GetClassName(),1,false);
				removeWeapon(ww);
				normalizeWeaponsID(1);
				return true;
			}
		}
	}
	return false;
}

// Equip or Add
bool equipWeaponInv(int ind, MechHardpoint hp, int grp, bool move=true)
{
	// Equip weapon from items
	class<Mechbase_Weapon> weap = inv_context.weapon_items[ind-1].Tag;
	if(!weap) return false;
	
	if( MDMath.RoundFloat(inv_context.weapons_tonnage+GetDefaultByType(weap).weight) > mech_tonnage && !CheckCompensative())
	{
		A_StartLocalSound("UI/Error",CHAN_BODY);
		Mechlab_Alert("\c[DARKRED]Cannot equip weapon, over tonnage!");
		return false;
	}
	if( hp.getFreeSlots() < GetDefaultByType(weap).hardslots )
	{
		A_StartLocalSound("UI/Error",CHAN_BODY);
		Mechlab_Alert("\c[DARKRED]Cannot equip weapon, no freeslots!");
		return false;	
	}
	 
	 // Equip weapon in inventory, don't give ammo.
	if(!equipWeapon(weap,hp,grp,false)) return false;
	if(move)inv_context.RemoveItem(ind-1, 1);
	return true;
}
bool equipWeapon(class<Mechbase_Weapon> type, MechHardpoint hp, int group, bool giveammo = true)
{
	if(checkVoodoo()) return false;
	GiveLocalInv(type,1);
	inv_context.weapons_count++;
	for(let w = Inv; w != null; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww)
		{
			if(ww.WeaponID == 0)
			{
				//console.printf("Setting new ID: %d",inv_context.weapons_count); 
				ww.WeaponID = inv_context.weapons_count;
				ww.group = group;
				ww.hardpoint = hp;
				hp.adjustHardslots(ww.hardslots);
				adjustTonnage(ww.weight);
				//console.printf("Adding Weapon: %s at %d",ww.displayName,ww.WeaponID);
			}
		}
	}
	inv_context.maxgroup = getMaxWeaponGrouping();
	return true;
}
void addWeaponToInv(String weapon,int amt=1,bool giveammo = true)
{
	if(inv_context.weapon_items.Size() == 0) inv_context.weapon_items.Clear();
	class<Mechbase_Weapon> weap = weapon;
	class<Mechbase_Utility> util = weapon;	
	for(int i = 0; i < amt; i++)inv_context.GiveItem(weapon);
}

// Next
void nextWeapon(bool gcycle=false)
{
	//console.printf("[4] %d",getNearestWeaponAt(getWeaponGroupAt(selectedWeapon)));
	if(weapon_groupfire || gcycle)
	{
		bool valid;
		int ind;
		[valid, ind] = getNextWeaponIn(selectedWeapon, getWeaponGroupAt(selectedWeapon));
		if(!valid)
		{
			selectedWeapon = getNearestWeaponAt(getWeaponGroupAt(selectedWeapon));
		}
		else
		{
			selectedWeapon = ind;
		}
	}
	else
	{
		selectedWeapon++;
	}
}

// Sort
void sortWeaponInv()
{
	Array<mw_InvStruct> newinv;
	Array<String> sortedtypes;
	GetItemsList(sortedtypes);
	newinv.Clear();
	String type;
	
	// Sort all items of unique types.
	for(int i = 0; i < sortedtypes.Size(); i++)
	{
		type = sortedtypes[i];
		type.MakeLower();
		for(int x = 0; x < inv_context.weapon_items.Size(); x++)
		{
			String thing = inv_context.weapon_items[x].Tag;
			thing.MakeLower();
			if(thing == type)
			{
				newinv.push(inv_context.weapon_items[x]);
			}
		}
	}
	// Re-allocate
	inv_context.weapon_items.Clear();
	inv_context.weapon_items.Copy(newinv);
}

// Fire
bool FireGroup(int group, bool alpha=false, bool forcegroup=false)
{
	if(checkVoodoo()) return false;
	if(group > getMaxWeaponGrouping()) return false;
	
	if(weapon_groupfire || forcegroup)
	{
		for(let w = Inv; w != null; w = w.Inv)
		{	
			let ww = Mechbase_Weapon(w);
			if(ww)
			{	
				if( (ww.group == group) || alpha) 
				{
					if(!ww.MWFire() && !weapons_fired) A_StartLocalSound("Mech/WeaponBusy", CHAN_OVERLAP);
					weapons_fired = true;
				}
			}
		}
	}
	else
	{
		int curgrp = getWeaponGroupAt(selectedWeapon);
		if(curgrp != group) selectedWeapon = getNearestWeaponAt(group);
		let weap = getSelectedWeapon();
		if(weap) 
		{
			if(!weap.MWFire() && !weapons_fired) A_StartLocalSound("Mech/WeaponBusy", CHAN_OVERLAP);
			weapons_fired = true;
		}
	}
	return true;
}
void FireWeapons(bool groupfire=true)
{
	if(checkVoodoo()) return;
	for(let w = Inv; w != null; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww)
		{	
			bool valid = groupfire ? (ww.group == getWeaponGroupAt(selectedWeapon)) : (ww.WeaponID == selectedWeapon);
			if(valid)
			{
				if(!ww.MWFire() && !weapons_fired) A_StartLocalSound("Mech/WeaponBusy", CHAN_OVERLAP);
				weapons_fired = true;
			}
		}
	}
}
bool FireWeaponByRef(Mechbase_Weapon ww)
{
	bool success = false;
	if(ww && !mech_shutdown) 
	{
		success = ww.MWFire();
		
		if(!success && !weapons_fired) A_StartLocalSound("Mech/WeaponBusy", CHAN_OVERLAP);
		weapons_fired = true;
	}
	return success;
}
void FireWeaponsList(Array<Mechbase_Weapon> wls, bool checkheat)
{
	for(int w = 0; w < wls.Size(); w++) FireWeaponByRef(wls[w]);
}
bool FindHomingWeapon(bool mustbeactive = false)
{
	for(let w = Inv; w != null; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww && ww.isHomming) 
		{
			if(!mustbeactive) return true; 
			if(ww.cooldown_timer > 0 && ww.volley_amt <= 0) return false;
			return true;
		}
	}
	return false;
}

// Find (AI protocols)
void GetAllWeapons(in out Array<Mechbase_Weapon> weaps)
{
	weaps.Clear();
	for(let w = Inv; w != null; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww) weaps.Push(ww);
	}
}
void GetOptimalWeapons(double range, in out Array<Mechbase_Weapon> weaps, bool fire, int chooseamt = 0, double heatperc = 0.36)
{
	weaps.Clear();
	int fired = 0;
	for(let w = Inv; w != null; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww)
		{
			if(ww.isHomming && !targetlocked) continue;
			
			let wobj = Mechbase_Actor(GetDefaultByType(ww.weapType));
			if(!wobj) continue;
			
			// Out of range checks
			if(wobj.minimum_range > 0 && range < wobj.minimum_range) 
				continue;	
			if(wobj.maximum_range > 0 && range > wobj.maximum_range)
				continue;
				
			// Cooldown check
			if(ww.cooldown_timer > 0)
				continue; // Weapon is busy
				
			// Heat check
			if(fire)
			{
				// Fire a randomly selected weapon.
				if(fired < chooseamt && random[AIWeapSel](0,2)==2 || !chooseamt)
				{
					if((heatlevel + wobj.heatgen) < MWHEAT_MAX*heatperc ) 
						if(FireWeaponByRef(ww)) fired++;
				}
			}
		
			// Add weapon to list
			weaps.Push(ww);
		}
	}
}
double FindAverageRange(Array<Mechbase_Weapon> from = NULL)
{
	double ranges;
	double weapcnt = 1; // Prevent division by 0
	if(!from)
	{
		for(let w = Inv; w != null; w = w.Inv)
		{	
			let ww = Mechbase_Weapon(w);
			if(ww) 
			{
				let wobj = Mechbase_Actor(GetDefaultByType(ww.weapType));
				if(wobj && wobj.maximum_range > 0) 
				{
					ranges += wobj.maximum_range;
					weapcnt++;
				}
			}
		}
	}
	else
	{
		for(int w = 0; w < from.Size(); w++) 
		{	
			let ww = from[w];
			let wobj = Mechbase_Actor(GetDefaultByType(ww.weapType));
			if(wobj && wobj.maximum_range > 0) 
			{
				ranges += wobj.maximum_range;
				weapcnt++;
			}
		}
	}
	return ranges/weapcnt;
}
double, double FindRanges(bool countminzero = false)
{
	double minrange = 0;
	double maxrange = 0;
	bool nominimum = false;
	for(let w = Inv; w != null; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww) 
		{
			let wobj = Mechbase_Actor(GetDefaultByType(ww.weapType));
			if(wobj && wobj.maximum_range > maxrange) maxrange = wobj.maximum_range;
			if(wobj && wobj.minimum_range > minrange) minrange = wobj.minimum_range;		
			if(wobj && wobj.minimum_range <= 0 && countminzero) nominimum = true;
		}
	}
	return nominimum ? 0 : minrange, maxrange;
}

/*
bool swapWeaponToInv(int weap_ind, int inv_ind)
{
	String weaptype;
	inv_ind --;
	
	// Make sure our inventory type is a weapon!
	class<Mechbase_Weapon> invweap = inv_context.weapon_items[inv_ind].Tag;
	if(!invweap) return false;
	
	for(let w = Inv; w; w = w.Inv)
	{	
		let ww = Mechbase_Weapon(w);
		if(ww)
		{
			if(weap_ind == ww.WeaponID)
			{
				weaptype = resolveWeaponName(ww);
				int prevweaps = inv_context.weapons_count;
				inv_context.weapons_count = ww.WeaponID-1;
				equipWeapon(inv_context.weapon_items[inv_ind].Tag,ww.hardpoint,ww.group,false);
				inv_context.weapons_count = prevweaps;
				removeWeapon(ww);
				inv_context.weapon_items[inv_ind] = weaptype;
				return true;
			}
		}
	}
	return false;
}
*/