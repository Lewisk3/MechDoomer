	/*****************************\
	|	 Miscellaneous Systems    |
	\*****************************/

// Mechbay Stuff
class Door_3D : Actor
{
	int up;

	Default
	{
		+NOBLOCKMAP;
	}
	
	override void tick()
	{
		vel.z = 0.6;
		up++;
		if(up > 255) 
		{
			Die(self,self);
		}
		super.tick();
	}

	States
	{
		Spawn:
			DOOR A 1;
		loop;
	}
}
class Mechbay_3D : Actor
{
	States
	{
		Spawn:
			MBAY A 1;
		loop;
	}
}	

extend class MechwarriorBase;

void startMechbay()
{
	let bay = Spawn("mechbay",pos);
	double xo = (cos(angle+12))*32;
	double yo = (sin(angle+12))*32;
	let door = Spawn("Door_3D",(pos.x+xo,pos.y+yo,pos.z+60));
	if(bay)bay.angle = angle;
	if(door)door.angle = angle+128;
	mech_shutdown = true;
	//mechsounds.pushSound("Mech/Betty/SystemsCheck");
	//A_PlaySound("Mech/Power",4);
	//mechsounds.pushSound("Mech/StartupDone");
	manualshutdown = true;
	prevticks = ticks;
	lockangle = angle;
	lockpitch = pitch;
}

void Mechlab_Alert(String str)
{
	hAlert.mechlab_text = str;
}

void Cockpit_Alert(String str, double time = 8.0)
{
	int msize = hAlert.cockpit_messages.Size();
	
	if(msize < 2)
	{
		if(msize == 0) 
			hAlert.mtimer1 = time;
		else
			hAlert.mtimer2 = time*1.25;
			
		hAlert.cockpit_messages.Push(str);
	}
	else 
	{
		hAlert.mtimer1 = time;
		hAlert.mtimer2 = time*1.5;
		hAlert.cockpit_messages[0] = str;
	}
}

void RadiusDamage(double dmg, double radius, Name type, class<Actor> ignoreactor = NULL)
{
	let it = ThinkerIterator.Create();
	Actor act;
	while(act = Actor(it.Next()))
	{
		if(ignoreactor && act is ignoreactor) continue;
		if(!act.bSHOOTABLE || act == self || Distance2D(act) > radius) continue;
		double rdmg = dmg*(1/(Distance2D(act)/radius));
		act.DamageMobJ(self, self, ceil(rdmg), type, 0, angle);
	}
}

bool adjustTonnage(double amt, bool force=false)
{
	if(MDMath.RoundFloat(inv_context.weapons_tonnage+amt) > mech_tonnage && !force && !CheckCompensative()) return false;
	inv_context.weapons_tonnage += amt;
	return true;
}

void adjustMoney(int amt) 
{ 
	if(!CheckVoodoo()) inv_context.money_cbills += amt; 
}

void A_StartLocalSound(Sound snd, uint channel = CHAN_BODY, uint flags = 0, double volume=1.0, double attn = ATTN_NORM, double pitch = 0.0)
{
	let cplr = MechwarriorBase(players[consoleplayer].mo);
	if(cplr && (cplr == self))  A_StartSound(snd, channel, flags, volume, attn, pitch);
}
