	/*************************\
	|	   Combat System      |
	\*************************/
extend class MechWarriorBase;

enum MechTypes
{
	MechType_Clan,
	MechType_Innersphere,
	MechType_BattleArmor
}

// Properties
double baseHeatEfficiency;
uint mechtype;

// Vars
Array<mw_CombatObjective> objectives;
bool display_objectives;
MechCamera dronecam;
RandomExplosions deathexplosions;
MechAI_Controller AICore;
bool dronecam_active;
bool heatwarn;
bool supersink;
bool InfraredActive;
bool ams_passive;
double HeatEfficiency;
double HeatLevel;
double prevheat;
double deltaHeat;
int radar_range; // Rader range mode; 0-3
int radar_scale; // Radar scales; 0-2
String crosshair_sprite;

// Crosshair(s)
void SetCrosshair(int ind)
{
	string cx = "";
	if(ind) cx = "XHAIRB"..ind;
	crosshair_sprite = cx;
}

// Drone
void ToggleDrone(double cdist, double rotspd, double angdist, bool front, bool alwayson=false)
{
	if(!dronecam)
	{
		dronecam = MechCamera.Create(self, cdist, angdist, rotspd, front);
		dronecam_active = true;
		if(!isdead)
		{
			mechSounds.PushSound("Mech/Blip");
			mechSounds.PushSound("Mech/Betty/ExternalCamera");
		}
	}
	else if(!alwayson)
	{
		dronecam_active = false;
	}
}
void AdjustDrone(double cdist, double rotspd, double angdist, bool front)
{
	if(dronecam && dronecam_active)
	{
		dronecam.followdist_desired = cdist;
		dronecam.rotspd = rotspd;
		dronecam.followtilt = angdist;
		dronecam.frontcam = front;
	}
}

// Death
void ProcessDeath(Actor killer)
{
	throttle = 0;
	if(isdead) return; 
	
	if(modelcockpit)
	{
		mechsounds.Empty();
		for(int i = 0; i < 5; i++) mechsounds.PushSound("Mech/Damaged/CriticalExplosion");
		deathexplosions = RandomExplosions.TriggerOnActor(self, 75,15,viewheight);
		if(!checkAI()) AdjustDrone(350, 1, 0, true);
		if(player && !checkAI()) player.cheats |= CF_TOTALLYFROZEN;
	}
	
	isdead = true;
	shutdown_setup(false, true, true);
}

// Main
void mech_weapons()
{
	// Calc HeatEff before player check because it's used for defaults.
	class<Heatsink> hs = "Heatsink";
	class<Heatsink> dhs = "DoubleHeatsink";
	double HSinkEff = (countStruct("Heatsink")*GetDefaultByType(hs).cooling/35)+(countStruct("DoubleHeatsink")*GetDefaultByType(dhs).cooling/35);
	HeatEfficiency = inv_context.heateff+baseHeatEfficiency+HSinkEff;

	// Process Heat levels and struct functionality.
	deltaHeat = MDMath.Lerp(deltaHeat, HeatLevel-prevheat, 0.5);
	process_antimissilesystem();
	
	// Targetting systems for AI
	process_targeting();
		
	Mech_HasJets = (countStruct("JumpJet") > 0);
	Mech_JetCount = countStruct("JumpJet");
	if(supersink) HeatEfficiency *= 3;
	// Dissipate heat quicker when under water
	if(waterlevel > 0) HeatEfficiency *= 2;
	if(HeatLevel > 0) HeatLevel -= HeatEfficiency;
	if(HeatLevel < 0) HeatLevel = 0; 
	if(HeatLevel > MWHEAT_MAX*0.53) DamageMobj(self,null,random(4,8),"overheating",DMG_NO_ARMOR | DMG_NO_FACTOR);
	if(HeatLevel <= MWHEAT_MAX*0.15) heatwarn = false;
	if(HeatLevel > MWHEAT_MAX*0.275 && !heatwarn)
	{
		A_StartLocalSound("Mech/HeatWarn", CHAN_VOICE, CHANF_OVERLAP);
		mechsounds.pushSound("Mech/Betty/HeatCritical");
		heatwarn = true;
	}
	if(HeatLevel >= MWHEAT_MAX*0.42 && !doShutdown && canShutdown) 
	{
		Cockpit_Alert("\c[SAPPHIRE]Shutdown sequence initiated!");
		mechsounds.pushSound("Mech/Betty/ShutdownInit");
		doShutdown = true;
		canShutdown = false;
		shutdown_timer = ticks;
	}
	if(HeatLevel <= MWHEAT_MAX*0.32) canShutdown = true;
	if( ((ticks - shutdown_timer) > MWHEAT_MAX) && doShutdown )
	{
		A_StopSound(CHAN_VOICE);
		Cockpit_Alert("\c[SAPPHIRE]Shutting down...");
		A_StartLocalSound("Mech/Betty/ShuttingDown", CHAN_VOICE);
		doShutdown = true;
		lockangle = angle;
		lockpitch = pitch;
		mech_shutdown = true;
		shutdown_timer = ticks;
		hAlert.hud_powersequence = MechStatusBar.GetAnimFlags();
		A_StartLocalSound("Mech/Shutdown", CHAN_VOICE);
	}
	if(HeatLevel > MWHEAT_MAX) HeatLevel = MWHEAT_MAX;
	
	if(selectedWeapon > inv_context.weapons_count)selectedWeapon = 1;
	if(selectedGroup > 4)selectedGroup = 1;
	
	if(!(level.maptime%8)) prevheat = HeatLevel;
	ticks++;
	
	// Return if invalid.
	if(!player || checkAI()) return;
	
	bool firing = (GetPlayerInput(INPUT_BUTTONS) & BT_ATTACK) || 
				  (GetPlayerInput(INPUT_BUTTONS) & BT_USER1 ) ||
				  (GetPlayerInput(INPUT_BUTTONS) & BT_USER2 ) ||
				  (GetPlayerInput(INPUT_BUTTONS) & BT_USER3 ) ||
				  (GetPlayerInput(INPUT_BUTTONS) & BT_USER4 );
	
	// Process hot/damaging floors. (Increase HeatLevel every 2 ticks)
	if( player.onground && CurSector.damageamount != 0 && !(level.maptime%2) )
	{
		HeatLevel += CurSector.damageamount * 0.01;
	}
	
	if( (GetPlayerInput(INPUT_BUTTONS) & BT_ATTACK) && getSelectedWeapon() && !pilotlook )
	{
		FireWeapons(weapon_groupfire);
	}
	if(!firing && weapons_fired && getSelectedWeapon() && !getSelectedWeapon().dofire) 
	{	
		nextWeapon(true);
		weapons_fired = false;
	}
	
	if(countinv("IBTN_SETWEAPGRP"))
	{	
		if(countinv("IBTN_SETWEAPGRP") > 4)
		{
			console.printf("ERROR: Invalid weapon group :> %d",countinv("IBTN_SETWEAPGRP"));
			TakeInventory("IBTN_SETWEAPGRP",999);
		}
		else
		{
			setWeaponGroupAt(selectedWeapon,countinv("IBTN_SETWEAPGRP"));
			inv_context.maxgroup = getMaxWeaponGrouping();
		}
		TakeInventory("IBTN_SETWEAPGRP",999);
	}
	
	if(countinv("IBTN_SHOOTALLWEAPONSLIKEAMADLAD") && !pilotlook)
	{
		FireGroup(0,true,true); // pew pew pew, whoosh, whoosh, whoosh, pow, pow, po.. *explodes*
		TakeInventory("IBTN_SHOOTALLWEAPONSLIKEAMADLAD",1);
	}
	
	if(countinv("IBTN_NXTGROUP"))
	{
		if(inv_context.weapons_count > 1)
		{
			int grp = getWeaponGroupAt(selectedWeapon)+1;
			if(grp > inv_context.maxgroup) grp = 1;
			selectedWeapon = getNearestWeaponAt(grp);
		}
		TakeInventory("IBTN_NXTGROUP",1);
	}
	
	if(countinv("IBTN_PRVGROUP"))
	{
		if(inv_context.weapons_count > 1)
		{
			int grp = getWeaponGroupAt(selectedWeapon)-1;
			if(grp < 1)grp = inv_context.weapons_count;
			selectedWeapon = getNearestWeaponAt(grp);
		}
		TakeInventory("IBTN_PRVGROUP",1);
	}
	
	if(countinv("IBTN_SHUTDOWN") || (!mech_shutdown && !inv_context.engine) )
	{
		Cockpit_Alert("\c[RED]Shutting down...");
		mechsounds.pushSound("Mech/Betty/ShuttingDown");
		A_StartLocalSound("Mech/Shutdown", CHAN_VOICE);
		mech_shutdown = true;
		manualshutdown = true;
		lockpitch = pitch;
		lockangle = angle;
		TakeInventory("IBTN_SHUTDOWN",1);
		hAlert.hud_powersequence = MechStatusBar.GetAnimFlags();
	}
		
	if(!targetenemy && targetreticle)
	{
		// Don't hold onto dead/invalid target.
		targetreticle.RDeconstruct();
		targetreticle = null;
	}
	
	if(countinv("IBTN_TARGET"))
	{
		TakeInventory("IBTN_TARGET",999);
		
		// Get reference to shootable actor under players aim.
		// AimLineAttack or AimTarget fail to respect viewheight, hence the LineTrace.
		//FLineTraceData lt;
		//LineTrace(angle, 9999, pitch, 0, viewheight, 0.0,0.0, lt);
		let c_aim = models_reference ? models_reference.cockpit.cockpitcam : NULL;
		double aimangle = c_aim ? c_aim.angle : angle;
		double aimpitch = c_aim ? c_aim.pitch : pitch;
		double twidth = 20;
		double steps = 16;
		MechTargetTracer lt;
		lt = MechTargetTracer.CreateTrace(self, Vec3Offset(0,0,viewheight),aimangle,aimpitch,81920.0);
		for(int i = 0; i < steps; i++)
		{
			if(!lt.hit_target)
			{
				double arc = (i/steps)*180;
				double aoffs = cos(180+arc);
				lt = MechTargetTracer.CreateTrace(self, Vec3Offset(0,0,viewheight),aimangle+(aoffs*twidth),aimpitch,81920.0);
			}
			if(lt.hit_target)
			{
				SoundEmitter.SE_Trigger("Mech/Target/Get",pos);
				if(targetreticle) targetreticle.RDeconstruct();
				targetenemy = lt.hit_target;
				double fdist = MechHitBox(targetenemy) ? 64 : 2;
				targetreticle = MechTargetReticle.InitCreate(self, targetenemy, 16, fdist);
				break;
			}
		}
	}
	
	if(countinv("IBTN_bFIREMODE"))
	{
		weapon_groupfire = !weapon_groupfire;
		if(weapon_groupfire)
		{
			Cockpit_Alert("\c[GREEN]Group Fire: Engaged");
			mechsounds.pushSound("Mech/Betty/Groupfire");
			mechsounds.pushSound("Mech/Betty/Engaged");
			TakeInventory("IBTN_bFIREMODE",1);
		} 
		else 
		{
			Cockpit_Alert("\c[GREEN]Chain Fire: Engaged");
			mechsounds.pushSound("Mech/Betty/Chainfire");
			mechsounds.pushSound("Mech/Betty/Engaged");
			TakeInventory("IBTN_bFIREMODE",1);
		}
		TakeInventory("IBTN_bFIREMODE",1);
	}
	
	if(!pilotlook)
	{
		if(GetPlayerInput(INPUT_BUTTONS) & BT_USER1) FireGroup(1);
		if(GetPlayerInput(INPUT_BUTTONS) & BT_USER2) FireGroup(2);
		if(GetPlayerInput(INPUT_BUTTONS) & BT_USER3) FireGroup(3);
		if(GetPlayerInput(INPUT_BUTTONS) & BT_USER4) FireGroup(4);
	}
	
	if(countinv("IBTN_NEXTWEAPON"))
	{	
		if(inv_context.weapons_count > 1)
		{
			A_StartLocalSound("Mech/NextWeapon",CHAN_WEAPON, CHANF_OVERLAP);
			selectedWeapon++;
		}
		if(selectedWeapon > inv_context.weapons_count) selectedWeapon = 1;
		TakeInventory("IBTN_NEXTWEAPON",1);
	}		
	if(countinv("IBTN_PREVWEAPON"))
	{
		if(inv_context.weapons_count > 1)
		{
			A_StartLocalSound("Mech/NextWeapon",CHAN_WEAPON, CHANF_OVERLAP);
			selectedWeapon--;
		}
		if(selectedWeapon < 1) selectedWeapon = inv_context.weapons_count;
		TakeInventory("IBTN_PREVWEAPON",1);
	}
}

// Homing weapons for AI
void process_targeting()
{
	let curweap = getSelectedWeapon();
	bool HasActiveHoming = FindHomingWeapon(true);
	bool target_inview = false;
	if(targetenemy is "NavPoint" || !curweap) return;
	double lockspd = checkAI() ? 2.4 : curweap.lockonspeed;
	
	if(!checkAI() && curweap && !curweap.isHomming)
	{
		// Discard lock if we're not a homing weapon and we've just been selected.
		locktimer = 0;
		targetlocked = false;
		return;
	}
	
	if(HasActiveHoming && targetenemy)
	{
		if(targetenemy.health <= 0) 
		{	
			targetenemy = null;
			return;
		}
		
		vector3 sight = level.SphericalCoords(
			Vec3Offset(0,0,viewheight), 
			targetenemy.Vec3Offset(0,0,targetenemy.height/2), 
			(angle,pitch)
		);
		target_inview = (abs(sight.x) < 30 && abs(sight.y) < 30);
		
		// Only lock if weapon is ready.
		if(target_inview)
		{
			if(locktimer == 0) A_StartLocalSound("Mech/Target/BeginLock",CHAN_WEAPON, CHANF_OVERLAP);
			if(locktimer < 100) locktimer += lockspd;
			if(locktimer > 90 && !targetlocked) 
			{
				targetlocked = true;
				prevlock = targetenemy;
				A_StartLocalSound("Mech/Target/Locked",CHAN_WEAPON, CHANF_OVERLAP);
			}
		}
		else
		{
			if(locktimer > 0) locktimer -= lockspd*2;
		}
	}
	else
	{
		// Deplete lock under conditions.
		if(locktimer > 0) locktimer -= lockspd*2;
	}
	
	if(locktimer < 100) targetlocked = false;				
	if(targetenemy && !prevlock) prevlock = targetenemy;
	if(targetlocked && targetenemy != prevlock) 
	{
		locktimer = 0;
		prevlock = NULL;
	}
}

// AMS
bool CheckHasAMS()
{
	for(int i = 0; i < inv_context.mechstructs.Size(); i++)
	{
		String cname = inv_context.mechstructs[i].Tag;
		class<AntiMissileSystem> basecast = cname;
		if(basecast) return true;
	}
	return false;
}
void process_antimissilesystem()
{
	if(ams_passive) return;
	for(int i = 0; i < inv_context.mechstructs.Size(); i++)
	{
		mw_InvStruct item = inv_context.mechstructs[i];
		String cname = item.Tag;
		class<AntiMissileSystem> basecast = cname;
		if(basecast)
		{
			trigger_ams(item);
			item.active_cooldown--;
		}
	}
}
void trigger_ams(mw_InvStruct amsitem)
{
	String cname = amsitem.Tag;
	vector2 amshardpoint = amsitem.hardpoint.pos;
	class<Mechbase_Utility> amsut = cname;
	class<AntiMissileSystem> amsdef = cname;
	
	let def_utams = GetDefaultByType(amsut);
	let def_ams = GetDefaultByType(amsdef);
	
	// Check ammunition.
	if(def_ams.ammotype) 
	{
		if(countammo(def_ams.ammotype) < def_ams.ammouse) return;
	}
	// Check if a projectile is near us.
	ThinkerIterator ams_it = ThinkerIterator.Create("Actor");
	Actor obj;
	while(obj = Actor(ams_it.Next()))
	{
		let mechweap = Mechbase_Actor(obj);
		if(mechweap && mechweap.mechplayer == self && !mechweap.mechplayer.CheckAI()) continue;
		if(obj is "AMS_Missile") continue;
		if(obj && amsitem.active_cooldown <= 0)
		{	
			if( (!mechweap && !obj.Damage) || !(mechweap is "MechMissile") ) continue;
			if(CheckSight(obj) && (obj.bMISSILE) && Distance3D(obj) < def_ams.activerange )
			{
				//console.printf("Firing AMS at: " .. obj.getTag());
				SoundEmitter.SE_Trigger(def_ams.firesound);
				amshardpoint.x += random(-5,5);
				amshardpoint.y += random(-5,5);
				vector2 offs = MDMath.VOffset(angle, 25, amshardpoint.x, 1);	
				double zoffs = player ? viewheight*player.crouchFactor : viewheight;
				vector3 lpos = Vec3Offset(offs.x,offs.y,zoffs+amshardpoint.y);
				obj.bSHOOTABLE = true;
				let ams = AMS_Missile(Spawn(def_ams.amstype,lpos));
				if(ams)
				{
					// AMS Sound FX
					ams.mechplayer = Mechwarrior(self);
					ams.tracer = obj;
					ams.master = self;
					amsitem.active_cooldown = def_ams.cooldown;
					ams.pitch = obj.pitch;
					ams.angle = ams.AngleTo(obj);
					// Will automatically set convengence factor in postplay.
					ams.vel = (
							cos(ams.AngleTo(obj))*cos(obj.pitch),
							sin(ams.AngleTo(obj))*cos(obj.pitch),
							-sin(obj.pitch)*5
							) * (obj.speed+def_ams.speed);
					if(def_ams.ammotype) TakeAmmo(def_ams.ammotype,def_ams.ammouse);
				}
			}
		}
	}
}